module

public import Veir.IR
public import Veir.Rewriter.InsertPoint
public import Veir.Rewriter.LinkedList

public section
namespace Veir

variable {OpInfo : Type} [HasOpInfo OpInfo]
variable {ctx : IRContext OpInfo}

/--
- Insert an operation at a given location.
-/
@[irreducible]
def Rewriter.insertOp? (ctx: IRContext OpInfo) (newOp: OperationPtr) (insertionPoint: InsertPoint)
    (newOpIn: newOp.InBounds ctx := by grind)
    (insIn : insertionPoint.InBounds ctx)
    (ctxInBounds: ctx.FieldsInBounds) : Option (IRContext OpInfo) :=
    rlet parent ← insertionPoint.block ctx
    let prev := insertionPoint.prev ctx (by grind)
    let next := insertionPoint.next
    newOp.linkBetweenWithParent ctx prev next parent (by grind) (by grind) (by grind) (by grind)

theorem Rewriter.insertOp?_inBounds_mono (ptr : GenericPtr)
    (heq : insertOp? ctx newOp ip h₁ h₂ h₃ = some newCtx) :
    ptr.InBounds newCtx ↔ ptr.InBounds ctx := by
  simp only [insertOp?] at heq
  grind

grind_pattern Rewriter.insertOp?_inBounds_mono =>
  Rewriter.insertOp? ctx newOp ip h₁ h₂ h₃, some newCtx, ptr.InBounds newCtx

@[grind .]
theorem Rewriter.insertOp?_fieldsInBounds_mono
    (heq : insertOp? ctx newOp ip h₁ h₂ h₃ = some newCtx) :
    ctx.FieldsInBounds → newCtx.FieldsInBounds := by
  simp only [insertOp?] at heq
  grind

/--
  Set the parent, previous, and next operation pointers of an operation to `none`.
  This method should not be used from outside the rewriter, and is only used to
  make proofs easier for grind.
-/
@[irreducible]
def Rewriter.unsetParentAndNeighbors (ctx : IRContext OpInfo) (op : OperationPtr) (hIn : op.InBounds ctx) :=
  let ctx := op.setParent ctx none
  let ctx := op.setPrevOp ctx none
  op.setNextOp ctx none

@[grind =]
theorem Rewriter.unsetParentAndNeighbors_inBounds (ptr : GenericPtr) :
    ptr.InBounds (unsetParentAndNeighbors ctx op hIn) ↔ ptr.InBounds ctx := by
  simp only [unsetParentAndNeighbors]
  grind

@[grind . ]
theorem Rewriter.unsetParentAndNeighbors_fieldsInBounds (hctx : ctx.FieldsInBounds) :
    (unsetParentAndNeighbors ctx op hIn).FieldsInBounds := by
  simp only [unsetParentAndNeighbors]
  grind

@[irreducible]
def Rewriter.detachOp (ctx: IRContext OpInfo) (op: OperationPtr) (hctx : ctx.FieldsInBounds) (hIn : op.InBounds ctx) (hasParent: (op.get ctx hIn).parent.isSome) : IRContext OpInfo :=
  let opStruct := op.get ctx
  let parent := opStruct.parent.get hasParent
  let ctx := unsetParentAndNeighbors ctx op hIn
  let prevOp := opStruct.prev
  let nextOp := opStruct.next
  -- I had to duplicate the continuation in each branch, I don't really
  -- know why the proofs of the preconditions in, say, `setNextOp` were
  -- metavariable... maybe somehow the execution of the tactics is slightly
  -- delayed?
  match _ : prevOp with
    | some prevOp =>
      let ctx := prevOp.setNextOp ctx nextOp
      match _ : nextOp with
      | some nextOp => nextOp.setPrevOp ctx prevOp (by grind (ematch := 10))
      | none => parent.setLastOp ctx prevOp (by grind (ematch := 10))
    | none =>
      let ctx := parent.setFirstOp ctx nextOp
      match _ : nextOp with
      | some nextOp => nextOp.setPrevOp ctx prevOp (by grind (ematch := 10))
      | none => parent.setLastOp ctx prevOp

@[grind .]
theorem Rewriter.detachOp_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachOp ctx hctx op hIn hasParent) ↔ ptr.InBounds ctx := by
  grind [detachOp]

@[grind .]
theorem Rewriter.detachOp_fieldsInBounds (hctx : ctx.FieldsInBounds) :
    (detachOp ctx op hctx hIn hasParent).FieldsInBounds := by
  simp only [detachOp]
  grind

/--
  Detach an operation from its parent if it has one.
  If it has no parent, return the context unchanged.
-/
@[irreducible, inline]
def Rewriter.detachOpIfAttached (ctx: IRContext OpInfo) (op: OperationPtr)
    (hctx : ctx.FieldsInBounds := by grind)
    (hop : op.InBounds ctx := by grind) : IRContext OpInfo :=
  match h: (op.get ctx hop).parent with
  | some _ => Rewriter.detachOp ctx op hctx hop (by grind)
  | none => ctx

@[grind .]
theorem Rewriter.detachOpIfAttached_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachOpIfAttached ctx op h₁ h₂) ↔ ptr.InBounds ctx := by
  grind [detachOpIfAttached]

@[grind .]
theorem Rewriter.detachOpIfAttached_fieldsInBounds (hctx : ctx.FieldsInBounds) :
    (detachOpIfAttached ctx op hctx hIn).FieldsInBounds := by
  grind [detachOpIfAttached]

@[irreducible, inline]
def Rewriter.detachOperands.loop (ctx : IRContext OpInfo) (op : OperationPtr) (index : Nat)
    (hCtx : ctx.FieldsInBounds := by grind)
    (hOp : op.InBounds ctx := by grind)
    (hIndex : index < op.getNumOperands! ctx := by grind) : IRContext OpInfo :=
  let ctx' := (OpOperandPtr.mk op index).removeFromCurrent ctx
  match index with
  | .succ index => Rewriter.detachOperands.loop ctx' op index (by grind) (by grind) (by grind)
  | 0 => ctx'

@[grind .]
theorem Rewriter.detachOperands.loop_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachOperands.loop ctx op index hCtx hOp hIndex) ↔ ptr.InBounds ctx := by
  induction index generalizing ctx <;> simp only [detachOperands.loop] <;> grind

@[grind .]
theorem Rewriter.detachOperands.loop_fieldsInBounds :
    ctx.FieldsInBounds → (detachOperands.loop ctx op index hCtx hOp hIndex).FieldsInBounds := by
  induction index generalizing ctx <;> simp only [detachOperands.loop] <;> grind

@[irreducible, inline]
def Rewriter.detachOperands (ctx : IRContext OpInfo) (op : OperationPtr)
    (hCtx : ctx.FieldsInBounds := by grind)
    (hOp : op.InBounds ctx := by grind) : IRContext OpInfo :=
  let numOperands := op.getNumOperands ctx (by grind)
  if h : numOperands = 0 then
    ctx
  else
    Rewriter.detachOperands.loop ctx op (numOperands - 1) (by grind) (by grind) (by grind)

@[grind .]
theorem Rewriter.detachOperands_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachOperands ctx op hCtx hOp) ↔ ptr.InBounds ctx := by
  grind [detachOperands]

@[grind .]
theorem Rewriter.detachOperands_fieldsInBounds :
    ctx.FieldsInBounds → (detachOperands ctx op hCtx hOp).FieldsInBounds := by
  grind [detachOperands]

@[irreducible, inline]
def Rewriter.detachBlockOperands.loop (ctx : IRContext OpInfo) (op : OperationPtr) (index : Nat)
    (hCtx : ctx.FieldsInBounds := by grind)
    (hOp : op.InBounds ctx := by grind)
    (hIndex : index < op.getNumSuccessors! ctx := by grind) : IRContext OpInfo :=
  let ctx' := (BlockOperandPtr.mk op index).removeFromCurrent ctx
  match index with
  | .succ index => Rewriter.detachBlockOperands.loop ctx' op index
  | 0 => ctx'

@[grind .]
theorem Rewriter.detachBlockOperands.loop_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachBlockOperands.loop ctx op index hCtx hOp hIndex) ↔ ptr.InBounds ctx := by
  induction index generalizing ctx <;> simp only [detachBlockOperands.loop] <;> grind

@[grind .]
theorem Rewriter.detachBlockOperands.loop_fieldsInBounds :
    ctx.FieldsInBounds → (detachBlockOperands.loop ctx op index hCtx hOp hIndex).FieldsInBounds := by
  induction index generalizing ctx <;> simp only [detachBlockOperands.loop] <;> grind

@[irreducible, inline]
def Rewriter.detachBlockOperands (ctx : IRContext OpInfo) (op : OperationPtr)
    (hCtx : ctx.FieldsInBounds := by grind)
    (hOp : op.InBounds ctx := by grind) : IRContext OpInfo :=
  let numOperands := op.getNumSuccessors ctx (by grind)
  if h : numOperands = 0 then
    ctx
  else
    Rewriter.detachBlockOperands.loop ctx op (numOperands - 1) (by grind) (by grind) (by grind)

@[grind .]
theorem Rewriter.detachBlockOperands_inBounds (ptr : GenericPtr) :
    ptr.InBounds (detachBlockOperands ctx op hCtx hOp) ↔ ptr.InBounds ctx := by
  grind [detachBlockOperands]

@[grind .]
theorem Rewriter.detachBlockOperands_fieldsInBounds :
    ctx.FieldsInBounds → (detachBlockOperands ctx op hCtx hOp).FieldsInBounds := by
  grind [detachBlockOperands]

@[irreducible, inline]
def Rewriter.eraseOp (ctx : IRContext OpInfo) (op : OperationPtr)
    (hCtx : ctx.FieldsInBounds := by grind)
    (hOp : op.InBounds ctx := by grind) : IRContext OpInfo :=
  let ctx := Rewriter.detachOpIfAttached ctx op
  let ctx := Rewriter.detachOperands ctx op
  let ctx := Rewriter.detachBlockOperands ctx op
  op.dealloc ctx

/-
Remark: the fact that `eraseOp` preserves `FieldsInBounds` relies on the fact
that the context is well formed.  Indeed, it is true because the only pointers
to the operands are in the doubly linked list that we patch.  I think in
addition we need to know that the results of the operation we are removing are
never used, which is ensured in the call to `eraseOp` in `replaceOp?` below for
example.
-/

/--
`eraseOp` only deallocates `op` and the pointers belonging to it (its results,
operands and block operands).  Hence, for any pointer that does not reference
`op`, being in bounds is unaffected by `eraseOp`.
-/
@[grind .]
theorem Rewriter.eraseOp_inBounds (ptr : GenericPtr)
    (hPtr : match ptr with
      | .operation op' => op' ≠ op
      | .opResult or => or.op ≠ op
      | .opOperand oo => oo.op ≠ op
      | .blockOperand bo => bo.op ≠ op
      | .value (.opResult or) => or.op ≠ op
      | .opOperandPtr (.operandNextUse oo) => oo.op ≠ op
      | .opOperandPtr (.valueFirstUse (.opResult or)) => or.op ≠ op
      | .blockOperandPtr (.blockOperandNextUse oo) => oo.op ≠ op
      | _ => True) :
    ptr.InBounds (eraseOp ctx op hCtx hOp) ↔ ptr.InBounds ctx := by
  grind [eraseOp]

/--
- Insert a block at a given location.
-/
@[irreducible]
def Rewriter.insertBlock? (ctx: IRContext OpInfo) (newBlock: BlockPtr)
    (insertionPoint: BlockInsertPoint)
    (newBlockIn: newBlock.InBounds ctx := by grind)
    (insIn : insertionPoint.InBounds ctx := by grind)
    (ctxInBounds: ctx.FieldsInBounds := by grind) : Option (IRContext OpInfo) :=
    rlet parent ← insertionPoint.region ctx
    let prev := insertionPoint.prev ctx (by grind)
    let next := insertionPoint.next
    newBlock.linkBetweenWithParent ctx prev next parent (by grind) (by grind) (by grind) (by grind)

@[grind .]
theorem Rewriter.insertBlock?_inBounds (ptr : GenericPtr)
    (heq : insertBlock? ctx newBlock ip h₁ h₂ h₃ = some newCtx) :
    ptr.InBounds ctx ↔ ptr.InBounds newCtx := by
  simp only [insertBlock?] at heq
  grind

@[grind .]
theorem Rewriter.insertBlock?_fieldsInBounds_mono
    (heq : insertBlock? ctx newBlock ip h₁ h₂ h₃ = some newCtx) :
    ctx.FieldsInBounds → newCtx.FieldsInBounds := by
  simp only [insertBlock?] at heq
  grind

def Rewriter.replaceUse (ctx: IRContext OpInfo) (use : OpOperandPtr) (newValue: ValuePtr)
    (useIn: use.InBounds ctx := by grind)
    (newIn: newValue.InBounds ctx := by grind)
    (ctxIn: ctx.FieldsInBounds := by grind) : IRContext OpInfo :=
  if (use.get ctx (by grind)).value = newValue then
    ctx
  else
    let ctx := use.removeFromCurrent ctx (by grind) (by grind)
    let ctx := use.setValue ctx newValue
    let ctx := use.insertIntoCurrent ctx (by grind) (by grind)
    ctx

@[grind =]
theorem Rewriter.replaceUse_inBounds (ptr : GenericPtr) :
    ptr.InBounds (replaceUse ctx use newValue useIn newIn ctxIn) ↔ ptr.InBounds ctx := by
  grind [replaceUse]

@[grind .]
theorem Rewriter.replaceUse_fieldsInBounds :
     ctx.FieldsInBounds → (replaceUse ctx use newValue useIn newIn ctxIn).FieldsInBounds := by
  grind [replaceUse]

/--
Set the type of a value (an op result or a block argument).
-/
def Rewriter.setType (ctx : IRContext OpInfo) (value : ValuePtr) (newType : TypeAttr)
    (valueIn : value.InBounds ctx := by grind) : IRContext OpInfo :=
  value.setType ctx newType

@[grind =]
theorem Rewriter.setType_inBounds (ptr : GenericPtr) :
    ptr.InBounds (setType ctx value newType valueIn) ↔ ptr.InBounds ctx := by
  grind [setType]

@[grind .]
theorem Rewriter.setType_fieldsInBounds :
    ctx.FieldsInBounds → (setType ctx value newType valueIn).FieldsInBounds := by
  grind [setType, ValuePtr.setType_fieldsInBounds]

@[irreducible]
def Rewriter.replaceValue? (ctx: IRContext OpInfo) (oldValue: ValuePtr) (newValue: ValuePtr)
    (oldIn: oldValue.InBounds ctx := by grind)
    (newIn: newValue.InBounds ctx := by grind)
    (ctxIn: ctx.FieldsInBounds := by grind)
    (depth: Nat := 1_000_000_000) : Option (IRContext OpInfo) :=
  match depth with
  | Nat.succ depth =>
    match _ : oldValue.getFirstUse ctx (by grind) with
    | none => ctx
    | some firstUse =>
    let ctx := Rewriter.replaceUse ctx firstUse newValue
    Rewriter.replaceValue? ctx oldValue newValue (by grind [Rewriter.replaceUse]) (by grind [Rewriter.replaceUse]) (by grind [Rewriter.replaceUse]) depth
  | _ => none

@[grind .]
theorem Rewriter.replaceValue?_inBounds (ptr : GenericPtr) :
    replaceValue? ctx old new h₁ h₂ h₃ d = some ctx' →
    (ptr.InBounds ctx ↔ ptr.InBounds ctx') := by
  induction d generalizing ctx
  case zero => simp [replaceValue?]
  case succ d ih => simp [replaceValue?]; split <;> grind [Rewriter.replaceUse]

@[grind .]
theorem Rewriter.replaceValue?_fieldsInBounds :
     ctx.FieldsInBounds → (replaceValue? ctx old new h₁ h₂ h₃ d).maybe₁ IRContext.FieldsInBounds := by
  induction d generalizing ctx
  case zero => simp [replaceValue?]
  case succ d ih => simp only [replaceValue?]; grind [Rewriter.replaceUse]

@[grind .]
theorem Rewriter.replaceValue?_preserves_results_size (op : OperationPtr) (hop : op.InBounds ctx) :
    replaceValue? ctx old new h₁ h₂ h₃ d = some ctx' →
    op.getNumResults! ctx' = op.getNumResults! ctx := by
  induction d generalizing ctx ctx'
  case zero => simp [replaceValue?, *]
  case succ d ih =>
    simp only [replaceValue?]; split
    · grind
    · rename_i firstUse hFirstUse
      intros hh
      rw [ih (ctx' := ctx') (ctx := Rewriter.replaceUse ctx firstUse new (by grind) (by grind) (by grind))] <;> grind [Rewriter.replaceUse]

@[grind .]
theorem Rewriter.replaceValue?_preserves_parent' (op : OperationPtr) (hop : op.InBounds ctx)
    (hctx' : replaceValue? ctx old new h₁ h₂ h₃ d = some ctx') :
    (op.get! ctx').parent = (op.get! ctx).parent := by
  induction d generalizing ctx
  case zero => simp [replaceValue?, *] at hctx' ⊢
  case succ d ih =>
    simp only [replaceValue?] at hctx'; split at hctx'
    · grind
    · rename_i firstUse hFirstUse
      rw [ih (ctx := Rewriter.replaceUse ctx firstUse new (by grind) (by grind) (by grind)) (by grind [Rewriter.replaceUse])] <;> grind [Rewriter.replaceUse]

@[grind .]
theorem Rewriter.replaceValue?_preserves_parent (op : OperationPtr) (hop : op.InBounds ctx)
    (hctx' : replaceValue? ctx old new h₁ h₂ h₃ d = some ctx') :
    (op.get ctx' (by grind)).parent = (op.get ctx hop).parent := by
  have := @replaceValue?_preserves_parent'
  grind [Rewriter.replaceUse]

@[irreducible]
def Rewriter.replaceOpResults (ctx: IRContext OpInfo) (fromOp toOp : OperationPtr)
  (index : Nat)
  (fromOpIB : fromOp.InBounds ctx := by grind) (toOpIB : toOp.InBounds ctx := by grind)
  (hNumFrom : fromOp.getNumResults! ctx ≥ index := by grind)
  (hNumTo : toOp.getNumResults! ctx ≥ index := by grind)
  (ctxInBounds : ctx.FieldsInBounds := by grind) : Option (IRContext OpInfo) :=
  match index with
  | 0 => ctx
  | index + 1 => do
    let oldResult := fromOp.getResult index
    let newResult := toOp.getResult index
    rlet ctx ← Rewriter.replaceValue? ctx oldResult newResult
    replaceOpResults ctx fromOp toOp index

theorem Rewriter.replaceOpResults_inBounds {ptr : GenericPtr} :
    replaceOpResults ctx fromOp toOp index fromOpIB toOpIB hNumFrom hNumTo ctxInBounds = some newCtx →
    (ptr.InBounds ctx ↔ ptr.InBounds newCtx) := by
  induction index generalizing ctx
  · grind [replaceOpResults]
  · simp only [replaceOpResults]
    grind

grind_pattern Rewriter.replaceOpResults_inBounds =>
  Rewriter.replaceOpResults ctx fromOp toOp index fromOpIB toOpIB hNumFrom hNumTo ctxInBounds,
  some newCtx, ptr.InBounds ctx
grind_pattern Rewriter.replaceOpResults_inBounds =>
  Rewriter.replaceOpResults ctx fromOp toOp index fromOpIB toOpIB hNumFrom hNumTo ctxInBounds,
  some newCtx, ptr.InBounds newCtx

@[grind <=]
theorem Rewriter.replaceOpResults_fieldsInBounds :
    replaceOpResults ctx fromOp toOp index fromOpIB toOpIB hNumFrom hNumTo ctxInBounds = some newCtx →
    newCtx.FieldsInBounds := by
  induction index generalizing ctx
  · grind [replaceOpResults]
  · simp only [replaceOpResults]
    grind

@[irreducible]
def Rewriter.replaceOp? (ctx: IRContext OpInfo) (oldOp newOp: OperationPtr)
    (oldIn: oldOp.InBounds ctx := by grind)
    (newIn: newOp.InBounds ctx := by grind)
    (ctxIn: ctx.FieldsInBounds := by grind)
    (_hpar : (oldOp.get ctx).parent.isSome = true) : Option (IRContext OpInfo) := do
  let numOldResults := oldOp.getNumResults ctx (by grind)
  let numNewResults := newOp.getNumResults ctx (by grind)
  if h : numOldResults ≠ numNewResults then
    none
  else
    rlet newCtx ← replaceOpResults ctx oldOp newOp numOldResults
    eraseOp newCtx oldOp

/--
`replaceOp?` do not allocate anything and only deallocates `op` and the pointers belonging to it
(its results, operands and block operands).  Hence, for any pointer that does not reference
`op`, being in bounds is unaffected by `eraseOp`.
-/
theorem Rewriter.replaceOp?_inBounds (ptr : GenericPtr)
    (h : Rewriter.replaceOp? ctx oldOp newOp oldIn newIn ctxIn hpar = some newCtx)
    (hPtr : match ptr with
      | .operation op' => op' ≠ oldOp
      | .opResult or => or.op ≠ oldOp
      | .opOperand oo => oo.op ≠ oldOp
      | .blockOperand bo => bo.op ≠ oldOp
      | .value (.opResult or) => or.op ≠ oldOp
      | .opOperandPtr (.operandNextUse oo) => oo.op ≠ oldOp
      | .opOperandPtr (.valueFirstUse (.opResult or)) => or.op ≠ oldOp
      | .blockOperandPtr (.blockOperandNextUse oo) => oo.op ≠ oldOp
      | _ => True) :
    ptr.InBounds newCtx ↔ ptr.InBounds ctx := by
  simp only [Rewriter.replaceOp?] at h
  grind

grind_pattern Rewriter.replaceOp?_inBounds =>
  Rewriter.replaceOp? ctx oldOp newOp oldIn newIn ctxIn hpar,
  some newCtx, ptr.InBounds newCtx

@[irreducible]
protected def Rewriter.pushBlockArgument (ctx : IRContext OpInfo) (blockPtr : BlockPtr) (type : TypeAttr)
    (blockPtrInBounds : blockPtr.InBounds ctx := by grind) : IRContext OpInfo :=
  let index := blockPtr.getNumArguments ctx (by grind)
  let argument := { type := type, firstUse := none, index := index, loc := (), owner := blockPtr : BlockArgument }
  blockPtr.pushArgument ctx argument (by grind)

@[grind =]
theorem Rewriter.pushBlockArgument_inBounds (ptr : GenericPtr) :
    ptr.InBounds (Rewriter.pushBlockArgument ctx blockPtr type blockPtrInBounds) ↔
    match ptr with
    | .blockArgument argPtr =>
      argPtr.InBounds ctx ∨ argPtr = blockPtr.nextArgument ctx
    | .value (.blockArgument argPtr) =>
      argPtr.InBounds ctx ∨ argPtr = blockPtr.nextArgument ctx
    | .opOperandPtr (.valueFirstUse (.blockArgument argPtr)) =>
      argPtr.InBounds ctx ∨ argPtr = blockPtr.nextArgument ctx
    | _ => ptr.InBounds ctx := by
  constructor <;> grind [Rewriter.pushBlockArgument]

@[grind .]
theorem Rewriter.pushBlockArgument_fieldsInBounds (hctx : ctx.FieldsInBounds) :
    (Rewriter.pushBlockArgument ctx blockPtr type blockPtrInBounds).FieldsInBounds := by
  simp only [Rewriter.pushBlockArgument]
  apply BlockPtr.pushArgument_fieldsInBounds
  · constructor <;> grind
  · exact hctx

def Rewriter.initBlockArguments (ctx: IRContext OpInfo) (blockPtr: BlockPtr) (types: Array TypeAttr)
    (index: Nat := 0) (hblock : blockPtr.InBounds ctx := by grind)
    (hidx : index = blockPtr.getNumArguments ctx := by grind) : IRContext OpInfo :=
  if h: index >= types.size then
    ctx
  else
    let ctx := Rewriter.pushBlockArgument ctx blockPtr types[index]
    Rewriter.initBlockArguments ctx blockPtr types (index + 1) (hidx := by grind [Rewriter.pushBlockArgument])
  termination_by types.size - index
  decreasing_by lia

@[grind .]
theorem Rewriter.initBlockArguments_fieldsInBounds (hx : ctx.FieldsInBounds) :
    (initBlockArguments ctx blockPtr types index h₁ h₂).FieldsInBounds := by
  fun_induction initBlockArguments <;> grind

/--
This theorem is a more general version of `initBlockArguments_inBounds` that is needed for the
induction step in the proof of that theorem.
-/
theorem Rewriter.initBlockArguments_inBounds' (ptr : GenericPtr) :
    ptr.InBounds (initBlockArguments ctx blockPtr types index h₁ h₂) ↔
    match ptr with
    | .blockArgument argPtr
    | .value (.blockArgument argPtr)
    | .opOperandPtr (.valueFirstUse (.blockArgument argPtr)) =>
      if argPtr.block = blockPtr then
        /-
          We need `argPtr.index < blockPtr.getNumArguments! ctx` because of the case where we
          call `initBlockArguments` on a block that already has a larger number of arguments as
          `types.size`.
        -/
        argPtr.index < types.size ∨ argPtr.index < blockPtr.getNumArguments! ctx
      else
        argPtr.InBounds ctx
    | _ => ptr.InBounds ctx := by
  fun_induction initBlockArguments <;> grind [BlockArgumentPtr.inBounds_def]

@[grind =]
theorem Rewriter.initBlockArguments_inBounds (ptr : GenericPtr) :
    ptr.InBounds (initBlockArguments ctx blockPtr types 0 h₁ h₂) ↔
    match ptr with
    | .blockArgument argPtr
    | .value (.blockArgument argPtr)
    | .opOperandPtr (.valueFirstUse (.blockArgument argPtr)) =>
      if argPtr.block = blockPtr then argPtr.index < types.size else argPtr.InBounds ctx
    | _ => ptr.InBounds ctx := by
  grind [Rewriter.initBlockArguments_inBounds']

@[grind .]
theorem Rewriter.initBlockArguments_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (initBlockArguments ctx blockPtr types index h₁ h₂) := by
  fun_induction initBlockArguments <;> grind

@[irreducible]
def Rewriter.createBlock (ctx: IRContext OpInfo) (argTypes : Array TypeAttr)
    (insertionPoint: Option BlockInsertPoint)
    (hctx : ctx.FieldsInBounds) (hip : insertionPoint.maybe BlockInsertPoint.InBounds ctx)
    : Option (IRContext OpInfo × BlockPtr) :=
  rlet (ctx, newBlockPtr) ← BlockPtr.allocEmpty ctx
  let ctx := Rewriter.initBlockArguments ctx newBlockPtr argTypes
  match h : insertionPoint with
  | some insertionPoint => do
    let ctx ← Rewriter.insertBlock? ctx newBlockPtr insertionPoint
      (by grind) (by grind [cases BlockInsertPoint]) (by grind)
    (ctx, newBlockPtr)
  | none =>
    (ctx, newBlockPtr)

@[grind .]
theorem Rewriter.createBlock_inBounds_mono (ptr : GenericPtr) (heq : createBlock ctx types ip hctx hip = some ⟨newCtx, newPtr⟩) :
    ptr.InBounds ctx → ptr.InBounds newCtx := by
  simp only [createBlock] at heq
  split at heq
  · grind
  · split at heq
    · simp [Option.bind] at heq
      split at heq <;> grind
    · grind

/--
`createBlock` allocate a new block with its arguments. Thus, the only new pointers that are in
bounds in the new context and not in the old one are the block itself, its arguments, and links to
the block arguments.
-/
@[grind =>]
theorem Rewriter.createBlock_inBounds (ptr : GenericPtr)
    (h : Rewriter.createBlock ctx types ip hctx hip = some (newCtx, newBlock)) :
    ptr.InBounds newCtx ↔
    match ptr with
    | .blockArgument argPtr
    | .value (.blockArgument argPtr)
    | .opOperandPtr (.valueFirstUse (.blockArgument argPtr)) =>
      if argPtr.block = newBlock then argPtr.index < types.size else argPtr.InBounds ctx
    | _ =>
      ptr.InBounds ctx ∨ ptr = .block newBlock ∨
        ptr = .blockOperandPtr (BlockOperandPtrPtr.blockFirstUse newBlock) := by
  simp only [Rewriter.createBlock] at h
  simp only [Option.bind_eq_bind, Option.bind] at h
  split at h; grind
  split at h
  · split at h <;> grind
  · grind

@[grind .]
theorem Rewriter.createBlock_fieldsInBounds_mono
    (heq : createBlock ctx types ip hctx hip = some ⟨newCtx, newPtr⟩) :
    ctx.FieldsInBounds → newCtx.FieldsInBounds := by
  simp only [createBlock] at heq
  split at heq
  · grind
  · split at heq
    · simp [Option.bind] at heq
      split at heq <;> grind
    · grind

def Rewriter.setBlockArguments (ctx : IRContext OpInfo) (blockPtr : BlockPtr)
    (types : Array TypeAttr) (hblock : blockPtr.InBounds ctx := by grind) : IRContext OpInfo :=
  let numArgs := blockPtr.getNumArguments ctx (by grind)
  if _ : numArgs = 0 then
    Rewriter.initBlockArguments ctx blockPtr types
  else
    let ctx := blockPtr.setArguments ctx #[]
    Rewriter.initBlockArguments ctx blockPtr types

@[grind =]
theorem Rewriter.setBlockArguments_inBounds (ptr : GenericPtr) :
    ptr.InBounds (Rewriter.setBlockArguments ctx blockPtr types hblock) ↔
    match ptr with
    | .blockArgument argPtr
    | .value (.blockArgument argPtr)
    | .opOperandPtr (.valueFirstUse (.blockArgument argPtr)) =>
      if argPtr.block = blockPtr then argPtr.index < types.size else argPtr.InBounds ctx
    | _ => ptr.InBounds ctx := by
  grind [Rewriter.setBlockArguments]

/-!
`Rewriter.setBlockArguments` preserves `FieldsInBounds` only if the context is well-formed and no
previous block argument is used. Otherwise, another pointer could point to one of the block arguments.
-/

@[irreducible, grind]
def Rewriter.createRegion (ctx: IRContext OpInfo) : Option (IRContext OpInfo × RegionPtr) :=
  RegionPtr.allocEmpty ctx

@[grind .]
theorem Rewriter.createRegion_new_inBounds (h : createRegion ctx = some (ctx', reg)) :
    reg.InBounds ctx' := by
  grind [createRegion]

@[grind .]
theorem Rewriter.createRegion_new_not_inBounds (h : createRegion ctx = some (ctx', reg)) :
    ¬ reg.InBounds ctx := by
  grind [createRegion]

@[grind =>]
theorem Rewriter.createRegion_genericPtr_mono (ptr : GenericPtr) (heq : createRegion ctx = some (ctx', ptr')) :
    ptr.InBounds ctx' ↔ (ptr.InBounds ctx ∨ ptr = .region ptr') := by
  grind [createRegion]

@[grind .]
theorem Rewriter.createRegion_fieldsInBounds (h : createRegion ctx = some (ctx', rg)) :
    ctx.FieldsInBounds → ctx'.FieldsInBounds := by
  grind [createRegion]

set_option linter.unusedVariables false
def Rewriter.pushRegion (ctx : IRContext OpInfo) (op : OperationPtr) (region : RegionPtr)
    (hop : op.InBounds ctx := by grind) (hregion : region.InBounds ctx := by grind)
    (hRegionParent : (region.get! ctx).parent = none := by grind) :
    IRContext OpInfo :=
  let ctx := region.setParent ctx op
  op.pushRegion ctx region

@[simp, grind =]
theorem Rewriter.pushRegion_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds (pushRegion ctx op region h₁ h₂ h₃) ↔ ptr.InBounds ctx := by
  simp only [pushRegion]
  grind

@[grind .]
theorem Rewriter.pushRegion_fieldsInBounds (hx : ctx.FieldsInBounds) :
    (pushRegion ctx op region h₁ h₂ h₃).FieldsInBounds := by
  simp only [pushRegion]
  apply OperationPtr.pushRegion_fieldsInBounds <;> grind

def Rewriter.initOpRegions (ctx: IRContext OpInfo) (opPtr: OperationPtr) (regions : Array RegionPtr) (index : Nat := 0)
    (opPtrInBounds : opPtr.InBounds ctx := by grind)
    (hregionInBounds : ∀ region ∈ regions, region.InBounds ctx := by grind)
    (hctx : ctx.FieldsInBounds := by grind) (hn : index = opPtr.getNumRegions ctx := by grind) : Option (IRContext OpInfo) :=
  if h: index >= regions.size then
    some ctx
  else
    let region := regions[index]
    if hParent : (region.get! ctx).parent = none then
      let ctx := pushRegion ctx opPtr region (hregion := by grind) (hRegionParent := hParent)
      Rewriter.initOpRegions ctx opPtr regions (index + 1)
        (hregionInBounds := by grind [Rewriter.pushRegion])
        (hn := by grind [Rewriter.pushRegion])
    else
      none
  termination_by regions.size - index
  decreasing_by lia

@[grind .]
theorem Rewriter.initOpRegions_fieldsInBounds {ctx' : IRContext OpInfo} :
    ctx.FieldsInBounds →
    initOpRegions ctx opPtr regions n opPtrInBounds hregions hctx hn = some ctx' →
    ctx'.FieldsInBounds := by
  fun_induction initOpRegions <;> grind

@[grind .]
theorem Rewriter.initOpRegions_inBounds_mono (ptr : GenericPtr) {ctx' : IRContext OpInfo} :
    ptr.InBounds ctx →
    initOpRegions ctx opPtr regions n opPtrInBounds hregions hctx hn = some ctx' →
    ptr.InBounds ctx' := by
  fun_induction initOpRegions <;> grind

def Rewriter.pushResult (ctx : IRContext OpInfo) (op : OperationPtr) (type : TypeAttr)
    (hop : op.InBounds ctx := by grind)
    : IRContext OpInfo :=
  let index := op.getNumResults! ctx
  let result : OpResult := { type := type, firstUse := none, index := index, owner := op }
  op.pushResult ctx result (by grind)

@[grind .]
theorem Rewriter.pushResult_fieldsInBounds (hx : ctx.FieldsInBounds) :
    (pushResult ctx op type hop).FieldsInBounds := by
    simp only [pushResult]
    apply OperationPtr.pushResult_fieldsInBounds
    · constructor <;> grind
    · grind

@[grind .]
theorem Rewriter.pushResult_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (pushResult ctx op type hop) := by
  grind [pushResult]

@[grind =]
theorem Rewriter.pushResult_inBounds (ptr : GenericPtr) :
    ptr.InBounds (pushResult ctx op type hop) ↔
    (ptr.InBounds ctx ∨
      ptr = .opResult (op.nextResult ctx) ∨
      ptr = .value (op.nextResult ctx) ∨
      ptr = .opOperandPtr (.valueFirstUse (op.nextResult ctx)))
    := by
  grind [pushResult]

def Rewriter.initOpResults (ctx: IRContext OpInfo) (opPtr: OperationPtr) (resultTypes: Array TypeAttr)
    (index: Nat := 0) (hop : opPtr.InBounds ctx)
    (hidx : index = opPtr.getNumResults ctx) : IRContext OpInfo :=
  if h: index >= resultTypes.size then
    ctx
  else
    let ctx := pushResult ctx opPtr resultTypes[index]
    Rewriter.initOpResults ctx opPtr resultTypes (index + 1) (by grind [pushResult]) (by grind [pushResult])
  termination_by resultTypes.size - index
  decreasing_by lia

@[grind .]
theorem Rewriter.initOpResults_fieldsInBounds (hx : ctx.FieldsInBounds) :
    (initOpResults ctx opPtr resultTypes index h₁ h₂).FieldsInBounds := by
  fun_induction initOpResults <;> grind

@[grind .]
theorem Rewriter.initOpResults_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (initOpResults ctx opPtr resultTypes index h₁ h₂) := by
  fun_induction initOpResults <;> grind

@[grind =]
theorem Rewriter.pushResult_inBounds_iff (ptr : GenericPtr) :
    ptr.InBounds (pushResult ctx op type hop) ↔
      (ptr.InBounds ctx ∨
       ptr = .opResult (op.nextResult ctx) ∨
       ptr = .value (op.nextResult ctx) ∨
       ptr = .opOperandPtr (.valueFirstUse (op.nextResult ctx))) := by
  simp [pushResult, OperationPtr.pushResult_genericPtr_mono]

@[irreducible]
protected def Rewriter.pushOperand (ctx : IRContext OpInfo) (opPtr : OperationPtr) (valuePtr : ValuePtr)
    (opPtrInBounds : opPtr.InBounds ctx := by grind) (valueInBounds : valuePtr.InBounds ctx := by grind) (hctx : ctx.FieldsInBounds) : IRContext OpInfo :=
  let op := (opPtr.get ctx (by grind))
  let index := opPtr.getNumOperands ctx (by grind)
  let operand := { value := valuePtr, owner := opPtr, back := OpOperandPtrPtr.valueFirstUse valuePtr, nextUse := none : OpOperand}
  have : operand.FieldsInBounds ctx := by constructor <;> grind
  let ctx := opPtr.pushOperand ctx operand (by grind)
  let ctx := (OpOperandPtr.mk opPtr index).insertIntoCurrent ctx (by grind) (by grind)
  ctx

@[grind .]
theorem Rewriter.pushOperand_inBounds (ptr : GenericPtr) :
    ptr.InBounds (Rewriter.pushOperand ctx opPtr valuePtr h₁ h₂ h₃) ↔
    (ptr.InBounds ctx ∨
     ptr = .opOperand (opPtr.nextOperand ctx) ∨
     ptr = .opOperandPtr (.operandNextUse (opPtr.nextOperand ctx))) := by
  constructor <;> grind [Rewriter.pushOperand]

@[simp, grind =]
theorem Rewriter.pushOperand_OperandPtr_InBounds_iff (valuePtr : ValuePtr) (hval : valuePtr.InBounds ctx) :
    ∀ (operandPtr : OpOperandPtr),
    (operandPtr.InBounds (Rewriter.pushOperand ctx opPtr valuePtr h₁ hval h₃)) ↔ ((operandPtr.InBounds ctx) ∨ operandPtr = opPtr.nextOperand ctx) := by
  simp only [Rewriter.pushOperand]
  simp [←GenericPtr.iff_opOperand]
  grind

@[grind .]
theorem Rewriter.pushOperand_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (Rewriter.pushOperand ctx opPtr valuePtr h₁ h₂ h₃) := by
  grind

@[grind .]
theorem Rewriter.pushOperand_fieldsInBounds :
    (Rewriter.pushOperand ctx opPtr valuePtr h₁ h₂ h₃).FieldsInBounds := by
  grind [Rewriter.pushOperand]

@[irreducible]
def Rewriter.initOpOperands (ctx: IRContext OpInfo) (opPtr: OperationPtr) (opPtrInBounds : opPtr.InBounds ctx)
    (operands : Array ValuePtr) (hoperands : ∀ oper, oper ∈ operands → oper.InBounds ctx) (hctx : ctx.FieldsInBounds)
    (n : Nat := operands.size) (hn : 0 ≤ n ∧ n ≤ operands.size := by grind) : IRContext OpInfo :=
  match h : n with
  | 0 => ctx
  | Nat.succ n' =>
    let index := operands.size - n
    let valuePtr := operands[index]'(by grind)
    let ctx := Rewriter.pushOperand ctx opPtr valuePtr (by grind) (by grind) (by grind)
    Rewriter.initOpOperands ctx opPtr (by grind) operands (by grind) (by grind) n' (by grind)

@[grind .]
theorem Rewriter.initOpOperands_fieldsInBounds :
    (initOpOperands ctx opPtr h₁ operands h₂ h₃ n hn).FieldsInBounds := by
  induction n generalizing ctx
  case zero => grind [initOpOperands]
  case succ n ih =>
    simp [initOpOperands]
    grind

@[grind .]
theorem Rewriter.initOpOperands_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (initOpOperands ctx opPtr h₁ operands h₂ h₃ n hn) := by
  induction n generalizing ctx
  case zero => grind [initOpOperands]
  case succ n ih =>
    simp [initOpOperands]
    grind


@[irreducible]
protected def Rewriter.pushBlockOperand (ctx : IRContext OpInfo) (opPtr : OperationPtr) (blockPtr : BlockPtr)
    (opPtrInBounds : opPtr.InBounds ctx := by grind) (blockInBounds : blockPtr.InBounds ctx := by grind)
    (hctx : ctx.FieldsInBounds := by grind) : IRContext OpInfo :=
  let op := (opPtr.get ctx (by grind))
  let index := opPtr.getNumSuccessors ctx (by grind)
  let operand := { value := blockPtr, owner := opPtr, back := BlockOperandPtrPtr.blockFirstUse blockPtr, nextUse := none : BlockOperand}
  have : operand.FieldsInBounds ctx := by constructor <;> grind
  let ctx := opPtr.pushBlockOperand ctx operand (by grind)
  let ctx := (BlockOperandPtr.mk opPtr index).insertIntoCurrent ctx (by grind) (by grind)
  ctx

@[grind =]
theorem Rewriter.pushBlockOperand_inBounds (ptr : GenericPtr) :
    ptr.InBounds (Rewriter.pushBlockOperand ctx opPtr valuePtr h₁ h₂ h₃) ↔
    (ptr.InBounds ctx ∨
      ptr = .blockOperand (opPtr.nextBlockOperand ctx) ∨
      ptr = .blockOperandPtr (.blockOperandNextUse (opPtr.nextBlockOperand ctx))) := by
  grind [Rewriter.pushBlockOperand]

@[grind .]
theorem Rewriter.pushBlockOperand_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (Rewriter.pushBlockOperand ctx opPtr valuePtr h₁ h₂ h₃) := by
  grind

@[grind .]
theorem Rewriter.pushBlockOperand_fieldsInBounds :
    (Rewriter.pushBlockOperand ctx opPtr valuePtr h₁ h₂ h₃).FieldsInBounds := by
  grind [Rewriter.pushBlockOperand]

@[irreducible]
def Rewriter.initBlockOperands (ctx: IRContext OpInfo) (opPtr: OperationPtr)
    (operands : Array BlockPtr) (n : Nat := operands.size) (opPtrInBounds : opPtr.InBounds ctx := by grind)
    (hctx : ctx.FieldsInBounds := by grind) (hoperands : ∀ oper, oper ∈ operands → oper.InBounds ctx := by grind)
    (hn : 0 ≤ n ∧ n ≤ operands.size := by grind) : IRContext OpInfo :=
  match h : n with
  | 0 => ctx
  | Nat.succ n' =>
    let index := operands.size - n
    let valuePtr := operands[index]'(by grind)
    let ctx := Rewriter.pushBlockOperand ctx opPtr valuePtr
    Rewriter.initBlockOperands ctx opPtr operands n'

@[grind .]
theorem Rewriter.initBlockOperands_fieldsInBounds :
    (initBlockOperands ctx opPtr operands n h₁ h₂ h₃ hn).FieldsInBounds := by
  induction n generalizing ctx
  case zero => grind [initBlockOperands]
  case succ n ih =>
    simp [initBlockOperands]
    grind

@[grind .]
theorem Rewriter.initBlockOperands_inBounds_mono (ptr : GenericPtr) :
    ptr.InBounds ctx → ptr.InBounds (initBlockOperands ctx opPtr operands n h₁ h₂ h₃ hn) := by
  induction n generalizing ctx
  case zero => grind [initBlockOperands]
  case succ n ih =>
    simp [initBlockOperands]
    grind

@[irreducible]
def Rewriter.createEmptyOp (ctx : IRContext OpInfo) (opType : OpInfo) (properties : HasOpInfo.propertiesOf opType) :
    Option (IRContext OpInfo × OperationPtr) :=
  OperationPtr.allocEmpty ctx opType properties

@[grind .]
theorem Rewriter.createEmptyOp_new_inBounds
    (h : createEmptyOp ctx opType properties = some (ctx', op)) :
    op.InBounds ctx' := by
  grind [createEmptyOp]

@[grind .]
theorem Rewriter.createEmptyOp_new_not_inBounds
    (h : createEmptyOp ctx opType properties = some (ctx', op)) :
    ¬ op.InBounds ctx := by
  grind [createEmptyOp]

@[grind =>]
theorem Rewriter.createEmptyOp_genericPtr_mono (ptr : GenericPtr)
    (heq : createEmptyOp ctx type properties = some (ctx', ptr')) :
    ptr.InBounds ctx' ↔ (ptr.InBounds ctx ∨ ptr = .operation ptr') := by
  grind [createEmptyOp]

@[grind .]
theorem Rewriter.createEmptyOp_fieldsInBounds
    (h : createEmptyOp ctx opType properties = some (ctx', op)) :
    ctx.FieldsInBounds → ctx'.FieldsInBounds := by
  grind [createEmptyOp]

@[irreducible]
def Rewriter.createOp (ctx: IRContext OpInfo) (opType: OpInfo)
    (resultTypes: Array TypeAttr) (operands: Array ValuePtr) (blockOperands : Array BlockPtr)
    (regions: Array RegionPtr) (properties: HasOpInfo.propertiesOf opType)
    (insertionPoint: Option InsertPoint)
    (hoper : ∀ oper, oper ∈ operands → oper.InBounds ctx := by grind)
    (hblockOperands : ∀ oper, oper ∈ blockOperands → oper.InBounds ctx := by grind)
    (hregions : ∀ reg, reg ∈ regions → reg.InBounds ctx := by grind)
    (hins : insertionPoint.maybe InsertPoint.InBounds ctx := by grind)
    (hx : ctx.FieldsInBounds := by grind) : Option (IRContext OpInfo × OperationPtr) :=
  rlet hnew : (ctx, newOpPtr) ← Rewriter.createEmptyOp ctx opType properties
  have hib : newOpPtr.InBounds ctx := by grind
  have : newOpPtr.getNumResults! ctx = 0 := by grind [createEmptyOp]
  have : newOpPtr.getNumRegions! ctx = 0 := by grind [createEmptyOp]
  let ctx := Rewriter.initOpResults ctx newOpPtr resultTypes 0 hib (by grind)
  have newOpPtrInBounds : newOpPtr.InBounds ctx := by grind
  rlet ctx ← Rewriter.initOpRegions ctx newOpPtr regions (newOpPtr.getNumRegions ctx)
  let ctx := Rewriter.initOpOperands ctx newOpPtr (by grind) operands (by grind) (by grind)
  let ctx := Rewriter.initBlockOperands ctx newOpPtr blockOperands (hoperands := by grind (ematch := 10))
  match _ : insertionPoint with
  | some insertionPoint =>
    rlet ctx ← Rewriter.insertOp? ctx newOpPtr insertionPoint (by grind)
      (by cases insertionPoint <;> grind) (by grind) in
    some (ctx, newOpPtr)
  | none =>
    (ctx, newOpPtr)

@[grind .]
theorem Rewriter.createOp_inBounds_mono (ptr : GenericPtr)
    (heq : createOp ctx opType numResults operands blockOperands regions props ip h₁ h₂ h₃ h₄ h₅ = some (newCtx, newOp)) :
    ptr.InBounds ctx → ptr.InBounds newCtx := by
  simp only [createOp] at heq
  grind (gen := 10)

@[grind .]
theorem Rewriter.createOp_new_inBounds (ptr : OperationPtr)
    (heq : createOp ctx opType numResults operands blockOperands regions props ip h₁ h₂ h₃ h₄ h₅ = some (newCtx, ptr)) :
    ptr.InBounds newCtx := by
  simp only [createOp] at heq
  grind

@[grind .]
theorem Rewriter.createOp_new_not_inBounds (ptr : OperationPtr)
    (heq : createOp ctx opType numResults operands blockOperands regions props ip h₁ h₂ h₃ h₄ h₅ = some (newCtx, ptr)) :
    ¬ ptr.InBounds ctx := by
  simp only [createOp] at heq
  grind

@[grind .]
theorem Rewriter.createOp_fieldsInBounds
    (heq : createOp ctx opType numResults operands blockOperands numRegions props ip h₁ h₂ h₃ h₄ h₅ = some (newCtx, newOp)) :
    ctx.FieldsInBounds → newCtx.FieldsInBounds := by
  simp only [createOp] at heq
  grind

@[irreducible]
def IRContext.create OpInfo [HasOpInfo OpInfo] : Option (IRContext OpInfo × OperationPtr) :=
  rlet (ctx, region) ← Rewriter.createRegion (empty OpInfo)
  rlet (ctx, operation) ← Rewriter.createOp ctx HasOpInfo.moduleOpCode #[] #[] #[] #[region] default none
  rlet (ctx, block) ← Rewriter.createBlock ctx #[] (some (.atEnd region)) (by grind) (by grind)
  return (ctx, operation)

@[grind →]
theorem IRContext.create_fieldsInBounds {op: OperationPtr} (h : IRContext.create OpInfo = some (ctx, op)) :
    ctx.FieldsInBounds := by
  simp only [IRContext.create] at h
  grind

@[grind →]
theorem IRContext.create_inBounds {op: OperationPtr} (h : IRContext.create OpInfo = some (ctx, op)) :
    op.InBounds ctx := by
  simp only [IRContext.create] at h
  grind (gen := 10)
