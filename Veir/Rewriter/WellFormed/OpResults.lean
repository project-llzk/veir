module

public import Veir.IR.Basic
public import Veir.Rewriter.Basic

import all Veir.Rewriter.Basic
import Veir.IR.WellFormed
import Veir.Rewriter.Basic
import Veir.Rewriter.GetSet

public section

namespace Veir

variable {OpInfo : Type} [HasOpInfo OpInfo]
variable {ctx : IRContext OpInfo}

/-! ## Rewriter.pushResult -/

theorem BlockPtr.opChain_Rewriter_pushResult
    (hWf : BlockPtr.OpChain block ctx array) :
    BlockPtr.OpChain block (Rewriter.pushResult ctx op type hop) array := by
  apply BlockPtr.OpChain_unchanged (ctx := ctx) <;> grind

theorem ValuePtr.defUse_Rewriter_pushResult
    (hWf : ValuePtr.DefUse value ctx array missingUses) :
    ValuePtr.DefUse value (Rewriter.pushResult ctx op type hop) array missingUses := by
  apply ValuePtr.DefUse.unchanged (ctx := ctx) <;> grind

theorem ValuePtr.defUse_Rewriter_pushResult_newResult (ctxFIB : ctx.FieldsInBounds) :
    ValuePtr.DefUse (op.nextResult ctx) (Rewriter.pushResult ctx op type hop) #[] ∅ := by
  constructor <;> grind

theorem BlockPtr.defUse_Rewriter_pushResult
    (hWf : BlockPtr.DefUse block ctx array missingUses) :
    BlockPtr.DefUse block (Rewriter.pushResult ctx op type hop) array missingUses := by
  apply BlockPtr.DefUse.unchanged (ctx := ctx) <;> grind

theorem RegionPtr.blockChain_Rewriter_pushResult
    (hWf : RegionPtr.BlockChain region ctx array) :
    RegionPtr.BlockChain region (Rewriter.pushResult ctx op type hop) array := by
  apply RegionPtr.blockChain_unchanged (ctx := ctx) hWf <;> grind

theorem IRContext.wellFormed_Rewriter_pushResult :
    ctx.WellFormed →
    (Rewriter.pushResult ctx op type hop).WellFormed := by
  intro wf
  have ⟨h₁, h₂, h₃, h₄, h₅, h₆, h₇, h₈⟩ := wf
  constructor
  case inBounds => grind
  case valueDefUseChains =>
    intros val hval
    have valCases : val.InBounds ctx ∨ val = op.nextResult ctx := by grind
    cases valCases
    case inl valInBounds =>
      have ⟨array, harray⟩ := h₂ val (by grind)
      exists array
      grind [ValuePtr.defUse_Rewriter_pushResult]
    case inr hvalEq =>
      grind [ValuePtr.defUse_Rewriter_pushResult_newResult]
  case blockDefUseChains =>
    intros block hblock
    have ⟨array, harray⟩ := h₃ block (by grind)
    exists array
    grind [BlockPtr.defUse_Rewriter_pushResult]
  case opChain =>
    intros block' hBlock'
    have ⟨array', harray'⟩ := h₄ block' (by grind)
    exists array'
    grind [BlockPtr.opChain_Rewriter_pushResult]
  case blockChain =>
    intros region hregion
    have ⟨array, harray⟩ := h₅ region (by grind)
    exists array
    apply RegionPtr.blockChain_unchanged harray <;> grind
  case operations =>
    intros op' hop'
    have : op'.InBounds ctx := by grind
    have ⟨ha, hb, hc, hd, he, hf, hg, hh⟩ := h₆ op' this
    constructor
    case region_parent =>
      intro region regionInBounds
      apply OperationPtr.WellFormed.region_parent.unchanged (ctx := ctx) <;> grind
    all_goals grind
  case blocks =>
    intros bl hbl
    have : bl.InBounds ctx := by grind
    grind [BlockPtr.WellFormed_unchanged]
  case regions =>
    grind [RegionPtr.WellFormed_unchanged]

theorem BlockPtr.operationList_rewriter_pushResult
    (ctxWf : ctx.WellFormed) :
    BlockPtr.operationList block' (Rewriter.pushResult ctx op type hop) newCtxWf blockInBounds' =
    BlockPtr.operationList block' ctx ctxWf (by grind) := by
  simp only [←BlockPtr.operationList_iff_BlockPtr_OpChain]
  grind [BlockPtr.opChain_Rewriter_pushResult]

grind_pattern BlockPtr.operationList_rewriter_pushResult =>
  Rewriter.pushResult ctx op type hop,
  (Rewriter.pushResult ctx op type hop).WellFormed,
  block'.operationList (Rewriter.pushResult ctx op type hop) (by grind) (by grind)

/-! ## Rewriter.initOpResults -/

theorem IRContext.wellFormed_rewriter_initOpResults :
    ctx.WellFormed →
    (Rewriter.initOpResults ctx opPtr resultTypes index hop hindex).WellFormed := by
  fun_induction Rewriter.initOpResults <;> grind [IRContext.wellFormed_Rewriter_pushResult]

theorem BlockPtr.opChain_rewriter_initOpResults
    (hWf : BlockPtr.OpChain block' ctx array) :
    BlockPtr.OpChain block'
      (Rewriter.initOpResults ctx op resultTypes index hop hidx) array := by
  fun_induction Rewriter.initOpResults <;>
    grind [BlockPtr.opChain_Rewriter_pushResult]

theorem BlockPtr.operationList_rewriter_initOpResults (ctxWf : ctx.WellFormed) :
    BlockPtr.operationList block'
      (Rewriter.initOpResults ctx op resultTypes index hop hidx) newCtxWf blockInBounds' =
    BlockPtr.operationList block' ctx ctxWf (by grind) := by
  simp only [←BlockPtr.operationList_iff_BlockPtr_OpChain]
  grind [BlockPtr.opChain_rewriter_initOpResults]

grind_pattern BlockPtr.operationList_rewriter_initOpResults =>
  (Rewriter.initOpResults ctx op resultTypes index hop hidx).WellFormed,
  block'.operationList (Rewriter.initOpResults ctx op resultTypes index hop hidx) newCtxWf blockInBounds'
