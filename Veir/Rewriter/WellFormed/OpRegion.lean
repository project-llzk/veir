module

public import Veir.IR.Basic
public import Veir.Rewriter.Basic

import all Veir.Rewriter.Basic
import Veir.IR.WellFormed
import Veir.Rewriter.GetSet

public section

namespace Veir

variable {OpInfo : Type} [HasOpInfo OpInfo]
variable {ctx : IRContext OpInfo}

theorem IRContext.wellFormed_Rewriter_pushRegion :
    ctx.WellFormed →
    (Rewriter.pushRegion ctx op region hop hregion hregionParent).WellFormed := by
  intro wf
  constructor
  case valueDefUseChains =>
    intros valuePtr valuePtrInBounds
    have ⟨array, arrayWf⟩ := wf.valueDefUseChains valuePtr (by grind)
    exists array
    apply ValuePtr.DefUse.unchanged (ctx := ctx) <;> grind
  case blockDefUseChains =>
    intros blockPtr blockPtrInBounds
    have ⟨array, arrayWf⟩ := wf.blockDefUseChains blockPtr (by grind)
    exists array
    apply BlockPtr.DefUse.unchanged (ctx := ctx) <;> grind
  case inBounds => grind
  case opChain =>
    intros blockPtr blockPtrInBounds
    have ⟨array, arrayWf⟩ := wf.opChain blockPtr (by grind)
    exists array
    apply BlockPtr.OpChain_unchanged (ctx := ctx) <;>
      grind
  case blockChain =>
    intros reg hreg
    have ⟨array, arrayWf⟩ := wf.blockChain reg (by grind)
    exists array
    apply RegionPtr.blockChain_unchanged (ctx := ctx) <;>
      grind
  case operations =>
    intros opPtr' opPtrInBounds
    have ⟨h₁, h₂, h₃, h₄, h₅, h₆, h₇, h₈⟩ := wf.operations opPtr' (by grind)
    constructor
    case region_parent =>
      intros region' region'InBounds
      constructor
      · grind
      · simp only [RegionPtr.parent!_pushRegion]
        split; rotate_left
        · simp only [OperationPtr.getRegion!_pushRegion]
          grind
        · intro _
          exists op.getNumRegions! ctx
          grind
    all_goals grind [OperationPtr.WellFormed]
  case blocks =>
    intros bl hbl
    have ⟨h₁, h₂, h₃, h₄, h₅⟩ := wf.blocks bl (by grind)
    constructor <;> grind
  case regions =>
    intros reg hreg
    have ⟨h₁, h₂⟩ := wf.regions reg (by grind)
    constructor
    · grind
    · simp only [RegionPtr.parent!_pushRegion]
      split
      · simp only [Option.some.injEq, forall_eq']
        exists op.getNumRegions! ctx
        grind
      · intro parent hparent
        have ⟨i, hi⟩ := h₂ hparent
        simp only [OperationPtr.getRegion!_pushRegion]
        grind

theorem Rewriter.initOpRegions_WellFormed (opPtr: OperationPtr)
    (hop : opPtr.InBounds ctx) (hfields : ctx.FieldsInBounds) (hctx : IRContext.WellFormed ctx) {hn}
    {ctx' : IRContext OpInfo}
    (h : Rewriter.initOpRegions ctx opPtr regions n hop regionInBounds hfields hn = some ctx') :
    ctx'.WellFormed := by
  fun_induction Rewriter.initOpRegions
  case case1 => grind
  case case2 => grind [IRContext.wellFormed_Rewriter_pushRegion]
  case case3 => grind

theorem BlockPtr.opChain_rewriter_pushRegion
    (hWf : BlockPtr.OpChain block' ctx array) :
    BlockPtr.OpChain block'
      (Rewriter.pushRegion ctx op region hop hregion hregionParent) array := by
  apply BlockPtr.OpChain_unchanged (ctx := ctx) <;> grind

theorem BlockPtr.operationList_rewriter_pushRegion
    (h : Rewriter.pushRegion ctx op region hop hregion hregionParent = newCtx)
    (ctxWf : ctx.WellFormed) :
    BlockPtr.operationList block' newCtx newCtxWf blockInBounds' =
    BlockPtr.operationList block' ctx ctxWf (by grind) := by
  simp only [←BlockPtr.operationList_iff_BlockPtr_OpChain]
  grind [BlockPtr.opChain_rewriter_pushRegion]

grind_pattern BlockPtr.operationList_rewriter_pushRegion =>
  Rewriter.pushRegion ctx op region hop hregion hregionParent,
  newCtx.WellFormed,
  block'.operationList newCtx newCtxWf blockInBounds'

theorem BlockPtr.opChain_rewriter_initOpRegions
    (h : Rewriter.initOpRegions ctx op regions index h₁ h₂ h₃ h₄ = some ctx')
    (hWf : BlockPtr.OpChain block' ctx array) :
    BlockPtr.OpChain block' ctx' array := by
  fun_induction Rewriter.initOpRegions generalizing array
  case case1 => grind
  case case2 => grind [BlockPtr.opChain_rewriter_pushRegion]
  case case3 => grind

theorem BlockPtr.operationList_rewriter_initOpRegions
    (h : Rewriter.initOpRegions ctx op regions index h₁ h₂ h₃ h₄ = some ctx')
    (ctxWf : ctx.WellFormed) :
    BlockPtr.operationList block' ctx' newCtxWf blockInBounds' =
    BlockPtr.operationList block' ctx ctxWf (by grind) := by
  simp only [←BlockPtr.operationList_iff_BlockPtr_OpChain]
  grind [BlockPtr.opChain_rewriter_initOpRegions]

grind_pattern BlockPtr.operationList_rewriter_initOpRegions =>
  Rewriter.initOpRegions ctx op regions index h₁ h₂ h₃ h₄, some ctx',
  block'.operationList ctx' newCtxWf blockInBounds'
