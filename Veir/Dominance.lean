module

public import Veir.Rewriter.InsertPoint
public import Veir.Dominance.Basic

/-!
  # Dominance

  This file is a placeholder for the dominance relation between IR constructs.
  It currently only contains axioms, and will be filled in with actual definitions and proofs
  in the future.

  This formalization assumes that all regions are SSACFG regions, so it particular it doesn't
  support graph regions.
-/

public section

namespace Veir

variable {OpInfo : Type} [HasOpInfo OpInfo]
variable {ctx : WfIRContext OpInfo}
variable {op op₁ op₂ : OperationPtr}

/--
  An operation `op₁` properly dominates an operation `op₂` if it dominates it
  and the operations are not equal.
-/
axiom OperationPtr.properlyDominates_def :
    op₁.ProperlyDominates op₂ ctx true ↔ op₁.Dominates op₂ ctx ∧ op₁ ≠ op₂

/--
  The dominance relation between an operation and an insertion point.
-/
axiom OperationPtr.dominatesIp (op : OperationPtr) (ip : InsertPoint) (ctx : WfIRContext OpInfo) : Prop

/--
  The dominance relation between a value and an insertion point.
-/
axiom ValuePtr.dominatesIp (val : ValuePtr) (ip : InsertPoint) (ctx : WfIRContext OpInfo) : Prop

/-!
## Lemmas about Dominance
-/

/--
An operation `op₁` dominates an operation `op₂` if it properly dominates it.
-/
axiom OperationPtr.dominates_of_properlyDominates :
    op₁.ProperlyDominates op₂ ctx true → op₁.Dominates op₂ ctx

/--
An operation dominates itself.
-/
@[grind .]
axiom OperationPtr.dominates_refl : op.Dominates op ctx

/--
An operation `op₁` dominates an operation `op₂` if and only if
`op₁` properly dominates `op₂` or if `op₁` is `op₂`.
-/
axiom OperationPtr.dominates_iff_properlyDominates_or_eq :
    op₁.Dominates op₂ ctx ↔ op₁.ProperlyDominates op₂ ctx true ∨ op₁ = op₂

/--
An operation `op₁` dominates the program point after a given operation `op₂` if it
either dominates the `op₂`, or is `op₂`.
-/
axiom OperationPtr.dominatesIp_iff :
    op₁.dominatesIp (InsertPoint.after op₂ ctx.raw block op₂HasParent op₂InBounds) ctx ↔
    op₁.Dominates op₂ ctx

/--
An operation `op₁` dominates the program point before `op₂` if it properly dominates `op₂`.
-/
@[simp]
axiom OperationPtr.dominatesIp_before :
  op₁.dominatesIp (.before op₂) ctx ↔ op₁.ProperlyDominates op₂ ctx true

grind_pattern OperationPtr.dominatesIp_before => op₁.dominatesIp (.before op₂) ctx

/--
Proper dominance between operations is transitive.
-/
axiom OperationPtr.properlyDominates_trans {op₃ : OperationPtr} :
  op₁.ProperlyDominates op₂ ctx true → op₂.ProperlyDominates op₃ ctx true →
  op₁.ProperlyDominates op₃ ctx true

/--
A value dominating the program point before an operation `op₁` also dominates the program
point before any operation `op₂` properly dominated by `op₁`.
-/
axiom ValuePtr.dominatesIp_before_of_properlyDominates {value : ValuePtr} :
  value.dominatesIp (InsertPoint.before op₁) ctx → op₁.ProperlyDominates op₂ ctx true →
  value.dominatesIp (InsertPoint.before op₂) ctx

/--
If an operation `op₁` dominates an operation `op₂`, it dominates the operation after `op₂`,
if it exists.
-/
axiom OperationPtr.dominates_next :
  op₁.Dominates op₂ ctx →
  (op₂.get! ctx.raw).next = some op₂Next →
  op₁.Dominates op₂Next ctx

/-!
## Programs Satisfying Dominance Invariants

This section defines `IRContext.Dom`, which ensure that the values in an `IRContext` respects
SSA dominance.
-/

/--
  A predicate that states that the values in the IR context are used in operations that
  are dominated by the operation or block that defines them.
-/
def WfIRContext.Dom (ctx : WfIRContext OpInfo) : Prop :=
  ∀ {op : OperationPtr} (_opInBounds : op.InBounds ctx.raw) {value : ValuePtr},
  value ∈ op.getOperands! ctx.raw →
  value.dominatesIp (InsertPoint.before op) ctx

/--
Operands of an operation are not results of dominated operations.
-/
axiom IRContext.Dom.value_not_in_results_of_forall_in_operands_of_dominates (ctxDom : ctx.Dom) :
    op₁.Dominates op₂ ctx →
    ∀ (value : ValuePtr), value ∈ op₁.getOperands! ctx.raw →
    value ∉ op₂.getResults! ctx.raw

/--
If a value is being defined by an operation `op₁` and being used as an operand of an
operation `op₂`, then `op₁` properly dominates `op₂`.
-/
axiom OperationPtr.properlyDominates_of_definingOp?_of_mem_getOperands! (ctxDom : ctx.Dom) :
  value.definingOp? = some op₁ →
  value ∈ op₂.getOperands! ctx.raw →
  op₁.ProperlyDominates op₂ ctx true

grind_pattern OperationPtr.properlyDominates_of_definingOp?_of_mem_getOperands! =>
  ctx.Dom, value.definingOp?, some op₂, op₁.getOperands! ctx.raw

/-- In a well-dominated IR context, any value that is an operand of an operation `op` is
dominating the program point before `op`. -/
@[grind →]
theorem WfIRContext.Dom.operand_dominates_op (ctxDom : ctx.Dom)
    (opInBounds : op.InBounds ctx.raw) :
    value ∈ op.getOperands! ctx.raw →
    value.dominatesIp (InsertPoint.before op) ctx := by
  grind [WfIRContext.Dom]

/-- In a well-dominated IR context, a value dominates the program point after an operation iff
it dominates the program point before the operation, or it is a result of the operation. -/
axiom WfIRContext.Dom.value_dominatesIp_after_iff (ctxDom : ctx.Dom) :
  value.dominatesIp (InsertPoint.after op ctx.raw block blockIsParent opInBounds) ctx ↔
  value.dominatesIp (InsertPoint.before op) ctx ∨ value ∈ op.getResults! ctx.raw

/-- A value dominating the entry of a successor block either already dominates the predecessor's
end, or it is one of the successor's own block arguments. -/
axiom WfIRContext.Dom.value_dominatesIp_successor_entry (ctxDom : ctx.Dom)
    {block : BlockPtr} (blockInBounds : block.InBounds ctx.raw)
    (hsucc : succ ∈ block.getSuccessors! ctx.raw) :
    value.dominatesIp (InsertPoint.atStart! succ ctx.raw) ctx →
    value.dominatesIp (InsertPoint.atEnd block) ctx ∨
      value ∈ succ.getArguments! ctx.raw

/-- An operation dominating the entry of a successor already dominates the predecessor's end. -/
axiom WfIRContext.Dom.op_dominatesIp_successor_entry (ctxDom : ctx.Dom)
    {block : BlockPtr} (blockInBounds : block.InBounds ctx.raw)
    (hsucc : succ ∈ block.getSuccessors! ctx.raw) :
    op.dominatesIp (InsertPoint.atStart! succ ctx.raw) ctx →
    op.dominatesIp (InsertPoint.atEnd block) ctx

/-- An argument of a block dominates the block's start. -/
axiom WfIRContext.Dom.blockArgument_dominatesIp_entry (ctxDom : ctx.Dom)
    {block : BlockPtr} (blockInBounds : block.InBounds ctx.raw)
    (hMem : value ∈ block.getArguments! ctx.raw) :
    value.dominatesIp (InsertPoint.atStart! block ctx.raw) ctx

/-- An argument of a block cannot dominate a program point that dominates the block start. -/
axiom WfIRContext.Dom.blockArgument_not_dominatesIp_before_of_dominatesIp_firstOp
    (ctxDom : ctx.Dom) {op : OperationPtr} (opInBounds : op.InBounds ctx.raw)
    (opDom : op.dominatesIp (InsertPoint.atStart! block ctx.raw) ctx)
    (hMem : value ∈ block.getArguments! ctx.raw) :
    ¬ value.dominatesIp (InsertPoint.before op) ctx
