module

public import Veir.IR.Basic
public import Veir.IR.Fields
import Veir.IR.GetSet
import Veir.IR.InBounds
import Veir.IR.Grind
import Veir.ForLean
import Std.Data.ExtHashSet

public section

namespace Veir

open ForLean

variable {OpInfo} [HasOpInfo OpInfo]
variable {ctx ctx' : IRContext OpInfo}

/--
  A def-use chain for an SSA value.
  The def-use chain is represented as an ordered array of operands, where
  each operand corresponds to a use of the value. The first element of the
  array is the first use of the value.
  Each operand in the array points to the next use of the value, forming a
  linked list.
-/
structure ValuePtr.DefUse
    (value : ValuePtr) (ctx : IRContext OpInfo) (array : Array OpOperandPtr)
    (missingUses : Std.ExtHashSet OpOperandPtr := ∅) : Prop where
  valueInBounds : value.InBounds ctx
  arrayInBounds (h : use ∈ array) : use.InBounds ctx
  firstElem : array[0]? = value.getFirstUse! ctx
  firstUseBack (heq : value.getFirstUse! ctx = some firstUse) :
    (firstUse.get! ctx).back = .valueFirstUse value
  allUsesInChain (use : OpOperandPtr) (huse : use.InBounds ctx) :
    (use.get! ctx).value = value → (use ∈ array ↔ use ∉ missingUses)
  useValue (hin : use ∈ array) : (use.get! ctx).value = value
  nextElems (hi : i < array.size) :
    (array[i].get! ctx).nextUse = array[i + 1]?
  prevNextUse (iPos : i > 0) (iInBounds : i < array.size) :
    (array[i].get! ctx).back = OpOperandPtrPtr.operandNextUse array[i - 1]
  missingUsesInBounds (hin : use ∈ missingUses) : use.InBounds ctx
  missingUsesValue (hin : use ∈ missingUses) : (use.get! ctx).value = value

attribute [grind →] ValuePtr.DefUse.valueInBounds
attribute [grind →] ValuePtr.DefUse.missingUsesInBounds
attribute [grind →] ValuePtr.DefUse.arrayInBounds

@[grind .]
theorem ValuePtr.DefUse_unique :
    ValuePtr.DefUse value ctx array missingUses →
    ValuePtr.DefUse value ctx array' missingUses →
    array = array' := by
  intros hWf hWf'
  apply Array.ext_getElem?
  intros i
  induction i <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse.unchanged
    (hWf : valuePtr.DefUse ctx array missingUses)
    (valuePtrInBounds' : valuePtr.InBounds ctx')
    (hSameFirstUse : valuePtr.getFirstUse! ctx = valuePtr.getFirstUse! ctx')
    (hPreservesInBounds : ∀ (usePtr : OpOperandPtr),
      usePtr.InBounds ctx →
      (usePtr.get! ctx).value = valuePtr → usePtr.InBounds ctx')
    (hSameUseFields : ∀ (usePtr : OpOperandPtr),
      usePtr.InBounds ctx → (usePtr.get! ctx).value = valuePtr →
      (usePtr.get! ctx') = (usePtr.get! ctx))
    (hPreservesInBounds' : ∀ (usePtr : OpOperandPtr),
      usePtr.InBounds ctx' →
      (usePtr.get! ctx').value = valuePtr →
      usePtr.InBounds ctx)
    (hSameUseFields' : ∀ (usePtr : OpOperandPtr),
      usePtr.InBounds ctx' →
      (usePtr.get! ctx').value = valuePtr →
      (usePtr.get! ctx) = (usePtr.get! ctx')) :
    valuePtr.DefUse ctx' array missingUses := by
  constructor <;> grind [ValuePtr.DefUse]

@[grind →]
theorem ValuePtr.DefUse.OpOperandPtr_value_of_getFirstUse
    {firstUse : OpOperandPtr} (hFirstUse : value.getFirstUse! ctx = some firstUse)
    (hDefUse : value.DefUse ctx array missingUses) :
    (firstUse.get! ctx).value = value := by
  grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse.ValuePtr_getFirstUse_ne_of_value_ne
    {use use' : OpOperandPtr}
    (valueNe : (use.get! ctx).value ≠ (use'.get! ctx).value)
    (hWF : (use.get! ctx).value.DefUse ctx array missingUses) :
    (use.get! ctx).value.getFirstUse! ctx ≠ some use' := by
  grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse_getFirstUse!_value_eq_of_back_eq_valueFirstUse
    {firstUse : OpOperandPtr} (hFirstUse : firstUse.InBounds ctx)
    (hvalueFirstUse : (firstUse.get! ctx).value.DefUse ctx array)
    (heq : (firstUse.get! ctx).back = .valueFirstUse value') :
    (firstUse.get! ctx).value.getFirstUse! ctx = some firstUse := by
  grind [ValuePtr.DefUse, Array.getElem?_of_mem]

theorem ValuePtr.DefUse.value!_eq_of_back!_eq_valueFirstUse
    {firstUse : OpOperandPtr}
    (hDefUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hInArray : firstUse ∈ array) :
    (firstUse.get! ctx).back = .valueFirstUse value →
    (firstUse.get! ctx).value = value := by
  have inArray : firstUse ∈ array := by grind [ValuePtr.DefUse]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem inArray
  cases i <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse.getFirstUse!_eq_of_back_eq_valueFirstUse
    {firstUse : OpOperandPtr}
    (hvalueFirstUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hInArray : firstUse ∈ array)
    (heq : (firstUse.get! ctx).back = .valueFirstUse value) :
    value.getFirstUse! ctx = some firstUse := by
  have : (firstUse.get! ctx).value = value := by grind [ValuePtr.DefUse.value!_eq_of_back!_eq_valueFirstUse]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem hInArray
  cases i <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse_back_eq_of_getFirstUse
    {firstUse : OpOperandPtr}
    (hvalueFirstUse : value.DefUse ctx array missingUses)
    (h : value.getFirstUse! ctx = some firstUse) :
    (firstUse.get! ctx).back = .valueFirstUse value := by
  have : (firstUse.get! ctx).value = value := by grind [ValuePtr.DefUse]
  grind [ValuePtr.DefUse, Array.getElem?_of_mem]

theorem ValuePtr.DefUse_getFirstUse!_eq_iff_back_eq_valueFirstUse
    {firstUse : OpOperandPtr}
    (hDefUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hFirstUse : firstUse ∈ array)
    (hDefUse' : value'.DefUse ctx array' missingUses') :
    (firstUse.get! ctx).back = .valueFirstUse value' ↔
    value'.getFirstUse! ctx = some firstUse := by
  constructor
  · grind [ValuePtr.DefUse, ValuePtr.DefUse.getFirstUse!_eq_of_back_eq_valueFirstUse]
  · grind [ValuePtr.DefUse, ValuePtr.DefUse_back_eq_of_getFirstUse]

theorem ValuePtr.DefUse_array_injective
    (hWF : ValuePtr.DefUse value ctx array hvalue) :
    ∀ (i j : Nat) iInBounds jInBounds, i ≠ j →
    array[i]'iInBounds ≠ array[j]'jInBounds := by
  intros i
  induction i
  · grind [ValuePtr.DefUse]
  · rintro (_|⟨_⟩) <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse_array_toList_Nodup
    (hWF : ValuePtr.DefUse value ctx array hvalue) :
    array.toList.Nodup := by
  simp only [List.nodup_iff_pairwise_ne]
  simp only [List.pairwise_iff_getElem]
  grind [ValuePtr.DefUse_array_injective]

@[grind .]
theorem ValuePtr.DefUse.array_mem_erase_self
    (hWF : ValuePtr.DefUse value ctx array hvalue) :
    use ∈ array → use ∉ array.erase use := by
  have := ValuePtr.DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.Nodup.not_mem_erase]

theorem ValuePtr.DefUse.array_mem_erase_getElem_self
    (hWF : ValuePtr.DefUse value ctx array hvalue) :
    ∀ (i : Nat) (iInBounds : i < array.size),
    array[i] ∉ array.erase array[i] := by
  have := ValuePtr.DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.Nodup.not_mem_erase]

theorem ValuePtr.DefUse_array_erase_array_index
    (hWF : ValuePtr.DefUse value ctx array hvalue) :
    ∀ (i : Nat) (iInBounds : i < array.size),
    array.idxOf array[i] = i := by
  have := ValuePtr.DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind  [List.idxOf_getElem]

@[grind .]
theorem ValuePtr.DefUse.erase_getElem_array_eq_eraseIdx :
    ValuePtr.DefUse value ctx array missingUses →
    (array.erase (array[i]'iInBounds)) = array.eraseIdx i iInBounds := by
  grind [Array.erase_eq_eraseIdx_of_idxOf, ValuePtr.DefUse_array_erase_array_index]

@[grind .]
theorem ValuePtr.DefUse.value!_eq_value!_of_nextUse!_eq {use : OpOperandPtr}
    (useInArray : use ∈ array)
    (useDefUse : (use.get! ctx).value.DefUse ctx array missingUses) :
    (use.get! ctx).nextUse = some use' →
    (use.get! ctx).value = (use'.get! ctx).value := by
  intros huse'
  have : use ∈ array := by grind
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  grind [ValuePtr.DefUse]

@[grind .]
theorem ValuePtr.DefUse.value!_eq_value!_of_back!_eq_operandNextUse
    {use : OpOperandPtr}
    (useInArray : use ∈ array)
    (useDefUse : (use.get! ctx).value.DefUse ctx array missingUses) :
    (use.get! ctx).back = .operandNextUse use' →
    (use.get! ctx).value = (use'.get! ctx).value := by
  intros huse'
  have : use ∈ array := by grind
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  cases i <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse.nextUse!_ne_of_getFirstUse!_eq {value : ValuePtr} {use : OpOperandPtr}
    (valueDefUse : ValuePtr.DefUse value ctx array missingUses)
    (useInArray : use ∈ array')
    (useDefUse : (use.get! ctx).value.DefUse ctx array' missingUses') :
    value.getFirstUse! ctx = some firstUse →
    (use.get! ctx).nextUse ≠ some firstUse := by
  intros hFirstUse hNextUse
  have : (use.get! ctx).value = (firstUse.get! ctx).value := by grind
  have : (use.get! ctx).value = value := by grind [ValuePtr.DefUse]
  subst value
  have : firstUse = array[0]'(by grind [ValuePtr.DefUse]) := by grind [ValuePtr.DefUse]
  have ⟨j, jInBounds, hj⟩ := Array.getElem_of_mem useInArray
  grind [ValuePtr.DefUse]

@[grind .]
theorem ValuePtr.DefUse.OpOperandPtr_setValue_self_of_value!_ne_self
    {use : OpOperandPtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ value) :
    value.DefUse ctx array missingUses →
    value.DefUse (use.setValue ctx value useInBounds) array (missingUses.insert use) := by
  intros hWF
  constructor <;> grind [ValuePtr.DefUse]

theorem ValuePtr.DefUse.OpOperandPtr_setValue_self_ofList_singleton_of_value!_ne_self
    {use : OpOperandPtr} {useInBounds} (useOfOtherValue : (use.get! ctx).value ≠ value) :
    value.DefUse ctx array →
    value.DefUse (use.setValue ctx value useInBounds) array (Std.ExtHashSet.ofList [use]) := by
  intros hWF
  constructor <;> grind [ValuePtr.DefUse]

@[grind .]
theorem ValuePtr.DefUse.OpOperandPtr_setValue_other_of_mem_missingUses
    {use : OpOperandPtr} {value value' : ValuePtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ value') {array} :
    use ∈ missingUses →
    value.DefUse ctx array missingUses →
    value.DefUse (use.setValue ctx value' useInBounds) array (missingUses.erase use) := by
  intros useNotMissing hWF
  constructor <;> grind [ValuePtr.DefUse]

@[grind .]
theorem ValuePtr.DefUse.OpOperandPtr_setValue_other_empty
    {use : OpOperandPtr} {value value' : ValuePtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ value') {array} :
    value.DefUse ctx array (Std.ExtHashSet.ofList [use]) →
    value.DefUse (use.setValue ctx value' useInBounds) array := by
  intros hWF
  constructor <;> grind [ValuePtr.DefUse]

@[grind .]
theorem ValuePtr.DefUse.OpOperandPtr_setValue_other_of_value_ne
    {ctx : IRContext OpInfo} {use : OpOperandPtr} {useInBounds} (value : ValuePtr)
    (useOfOtherValue' : (use.get! ctx).value ≠ value')
    (valueNe : value ≠ value') {array} :
    value'.DefUse ctx array missingUses →
    value'.DefUse (use.setValue ctx value useInBounds) array missingUses := by
  intro hWF
  apply ValuePtr.DefUse.unchanged (ctx := ctx) <;> grind

section BlockPtr.DefUse

structure BlockPtr.DefUse (blockPtr : BlockPtr) (ctx : IRContext OpInfo)
    (array : Array BlockOperandPtr) (missingUses : Std.ExtHashSet BlockOperandPtr := ∅) : Prop where
  blockInBounds : blockPtr.InBounds ctx
  arrayInBounds (h : use ∈ array) : use.InBounds ctx
  firstElem : array[0]? = (blockPtr.get! ctx).firstUse
  nextElems (hi : i < array.size) : ((array[i]'(by grind)).get! ctx).nextUse = array[i + 1]?
  useValue use (hu : use ∈ array) : (use.get! ctx).value = blockPtr
  firstUseBack (heq : (blockPtr.get! ctx).firstUse = some firstUse) :
    (firstUse.get! ctx).back = BlockOperandPtrPtr.blockFirstUse blockPtr
  backNextUse i (iPos : i > 0) (iInBounds : i < array.size) :
    (array[i].get! ctx).back = BlockOperandPtrPtr.blockOperandNextUse array[i - 1]
  allUsesInChain (use : BlockOperandPtr) (useInBounds : use.InBounds ctx) :
    (use.get! ctx).value = blockPtr → (use ∈ array ↔ use ∉ missingUses)
  missingUsesInBounds (hin : use ∈ missingUses) : use.InBounds ctx
  missingUsesValue (hin : use ∈ missingUses) : (use.get! ctx).value = blockPtr

attribute [local grind] BlockPtr.DefUse
attribute [grind →] BlockPtr.DefUse.blockInBounds
attribute [grind →] BlockPtr.DefUse.missingUsesInBounds
attribute [grind →] BlockPtr.DefUse.missingUsesValue
attribute [grind →] BlockPtr.DefUse.arrayInBounds

theorem BlockPtr.DefUse.unchanged
    (hWf : blockPtr.DefUse ctx array missingUses)
    (blockPtrInBounds' : blockPtr.InBounds ctx')
    (hSameFirstUse : (blockPtr.get! ctx).firstUse = (blockPtr.get! ctx').firstUse)
    (hPreservesInBounds : ∀ (usePtr : BlockOperandPtr),
      usePtr.InBounds ctx →
      (usePtr.get! ctx).value = blockPtr → usePtr.InBounds ctx')
    (hSameUseFields : ∀ (usePtr : BlockOperandPtr),
      usePtr.InBounds ctx → (usePtr.get! ctx).value = blockPtr →
      (usePtr.get! ctx') = (usePtr.get! ctx))
    (hPreservesInBounds' : ∀ (usePtr : BlockOperandPtr),
      usePtr.InBounds ctx' →
      (usePtr.get! ctx').value = blockPtr →
      usePtr.InBounds ctx)
    (hSameUseFields' : ∀ (usePtr : BlockOperandPtr),
      usePtr.InBounds ctx' →
      (usePtr.get! ctx').value = blockPtr →
      (usePtr.get! ctx) = (usePtr.get! ctx')) :
    blockPtr.DefUse ctx' array missingUses := by
  constructor <;> grind

theorem BlockPtr.DefUse.getFirstUse_ne_of_value_ne
    {use use' : BlockOperandPtr}
    (valueNe : (use.get! ctx).value ≠ (use'.get! ctx).value)
    (hWF : (use.get! ctx).value.DefUse ctx array missingUses) :
    ((use.get! ctx).value.get! ctx).firstUse ≠ some use' := by
  grind

theorem BlockPtr.DefUse.getFirstUse!_value_eq_of_back_eq_valueFirstUse
    {firstUse : BlockOperandPtr} (hFirstUse : firstUse.InBounds ctx)
    (hvalueFirstUse : (firstUse.get! ctx).value.DefUse ctx array)
    (heq : (firstUse.get! ctx).back = .blockFirstUse block) :
    ((firstUse.get! ctx).value.get! ctx).firstUse = some firstUse := by
  have : firstUse ∈ array := by grind [BlockPtr.DefUse]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  cases i <;> grind

theorem BlockPtr.DefUse.value!_eq_of_back!_eq_valueFirstUse
    {firstUse : BlockOperandPtr}
    (hDefUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hInArray : firstUse ∈ array) :
    (firstUse.get! ctx).back = .blockFirstUse block →
    (firstUse.get! ctx).value = block := by
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem hInArray
  cases i <;> grind

theorem BlockPtr.DefUse.getFirstUse!_eq_of_back_eq_valueFirstUse
    {firstUse : BlockOperandPtr}
    (hvalueFirstUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hInArray : firstUse ∈ array)
    (heq : (firstUse.get! ctx).back = .blockFirstUse block) :
    (block.get! ctx).firstUse = some firstUse := by
  have : (firstUse.get! ctx).value = block := by grind [BlockPtr.DefUse.value!_eq_of_back!_eq_valueFirstUse]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem hInArray
  cases i <;> grind [Array.getElem?_of_mem]

theorem BlockPtr.DefUse_back_eq_of_getFirstUse
    {firstUse : BlockOperandPtr}
    (hvalueFirstUse : block.DefUse ctx array missingUses)
    (h : (block.get! ctx).firstUse = some firstUse) :
    (firstUse.get! ctx).back = .blockFirstUse block := by
  have : (firstUse.get! ctx).value = block := by grind
  grind [Array.getElem?_of_mem]

theorem BlockPtr.DefUse.value_eq_of_getFirstUse
    (hvalueFirstUse : BlockPtr.DefUse block ctx array missingUses)
    (h : (block.get! ctx).firstUse = some firstUse) :
    (firstUse.get! ctx).value = block := by
  grind

grind_pattern BlockPtr.DefUse.value_eq_of_getFirstUse =>
  BlockPtr.DefUse block ctx array missingUses, (block.get! ctx).firstUse, (firstUse.get! ctx).value

theorem BlockPtr.DefUse_getFirstUse!_eq_iff_back_eq_valueFirstUse
    {firstUse : BlockOperandPtr}
    (hDefUse : (firstUse.get! ctx).value.DefUse ctx array missingUses)
    (hFirstUse : firstUse ∈ array)
    (hDefUse' : block'.DefUse ctx array' missingUses') :
    (firstUse.get! ctx).back = .blockFirstUse block' ↔
    (block'.get! ctx).firstUse = some firstUse := by
  constructor
  · grind [BlockPtr.DefUse.getFirstUse!_eq_of_back_eq_valueFirstUse]
  · grind [BlockPtr.DefUse_back_eq_of_getFirstUse]

theorem BlockPtr.DefUse_array_injective
    (hWF : BlockPtr.DefUse block ctx array missingUses) :
    ∀ (i j : Nat) iInBounds jInBounds, i ≠ j →
    array[i]'iInBounds ≠ array[j]'jInBounds := by
  intros i
  induction i
  · grind
  · rintro ⟨_|_⟩ <;> grind

theorem BlockPtr.DefUse_array_toList_Nodup
    (hWF : BlockPtr.DefUse block ctx array missingUses) :
    array.toList.Nodup := by
  simp only [List.nodup_iff_pairwise_ne]
  simp only [List.pairwise_iff_getElem]
  grind [DefUse_array_injective]

@[grind .]
theorem BlockPtr.DefUse.array_mem_erase_self
    (hWF : BlockPtr.DefUse value ctx array missingUses) :
    use ∈ array → use ∉ array.erase use := by
  have := DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.Nodup.not_mem_erase]

theorem BlockPtr.DefUse.array_mem_erase_getElem_self
    (hWF : BlockPtr.DefUse value ctx array missingUses) :
    ∀ (i : Nat) (iInBounds : i < array.size),
    array[i] ∉ array.erase array[i] := by
  have := DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.Nodup.not_mem_erase]

theorem BlockPtr.DefUse_array_erase_array_index
    (hWF : BlockPtr.DefUse value ctx array hvalue) :
    ∀ (i : Nat) (iInBounds : i < array.size),
    array.idxOf array[i] = i := by
  have := DefUse_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.idxOf_getElem]

@[grind .]
theorem BlockPtr.DefUse.erase_getElem_array_eq_eraseIdx :
    BlockPtr.DefUse value ctx array missingUses →
    (array.erase (array[i]'iInBounds)) = array.eraseIdx i iInBounds := by
  grind [Array.erase_eq_eraseIdx_of_idxOf, BlockPtr.DefUse_array_erase_array_index]

@[grind .]
theorem BlockPtr.DefUse.value!_eq_value!_of_nextUse!_eq {use : BlockOperandPtr}
    (useInArray : use ∈ array)
    (useDefUse : (use.get! ctx).value.DefUse ctx array missingUses) :
    (use.get! ctx).nextUse = some use' →
    (use.get! ctx).value = (use'.get! ctx).value := by
  intros huse'
  have : use ∈ array := by grind
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  grind

@[grind .]
theorem BlockPtr.DefUse.value!_eq_value!_of_back!_eq_operandNextUse
    {use : BlockOperandPtr}
    (useInArray : use ∈ array)
    (useDefUse : (use.get! ctx).value.DefUse ctx array missingUses) :
    (use.get! ctx).back = .blockOperandNextUse use' →
    (use.get! ctx).value = (use'.get! ctx).value := by
  intros huse'
  have : use ∈ array := by grind
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  cases i <;> grind

theorem BlockPtr.DefUse.nextUse!_ne_of_getFirstUse!_eq {value : BlockPtr} {use : BlockOperandPtr}
    (valueDefUse : BlockPtr.DefUse value ctx array missingUses)
    (useInArray : use ∈ array')
    (useDefUse : (use.get! ctx).value.DefUse ctx array' missingUses') :
    (value.get! ctx).firstUse = some firstUse →
    (use.get! ctx).nextUse ≠ some firstUse := by
  intros hFirstUse hNextUse
  have : (use.get! ctx).value = (firstUse.get! ctx).value := by grind
  have : (use.get! ctx).value = value := by grind
  subst value
  have : firstUse = array[0]'(by grind) := by grind
  have ⟨j, jInBounds, hj⟩ := Array.getElem_of_mem useInArray
  grind

@[grind .]
theorem BlockPtr.DefUse.OpOperandPtr_setValue_self_of_value!_ne_self
    {use : BlockOperandPtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ value) :
    value.DefUse ctx array missingUses →
    value.DefUse (use.setValue ctx value useInBounds) array (missingUses.insert use) := by
  intros hWF
  constructor <;> grind

theorem BlockPtr.DefUse.OpOperandPtr_setValue_self_ofList_singleton_of_value!_ne_self
    {use : BlockOperandPtr} {useInBounds} (useOfOtherValue : (use.get! ctx).value ≠ value) :
    value.DefUse ctx array →
    value.DefUse (use.setValue ctx value useInBounds) array (Std.ExtHashSet.ofList [use]) := by
  intros hWF
  constructor <;> grind

@[grind .]
theorem BlockPtr.DefUse.OpOperandPtr_setValue_other_of_mem_missingUses
    {use : BlockOperandPtr} {block block' : BlockPtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ block') {array} :
    use ∈ missingUses →
    block.DefUse ctx array missingUses →
    block.DefUse (use.setValue ctx block' useInBounds) array (missingUses.erase use) := by
  intros useNotMissing hWF
  constructor <;> grind

@[grind .]
theorem BlockPtr.DefUse.OpOperandPtr_setValue_other_empty
    {use : BlockOperandPtr} {block block' : BlockPtr} {useInBounds}
    (useOfOtherValue : (use.get! ctx).value ≠ block') {array} :
    block.DefUse ctx array (Std.ExtHashSet.ofList [use]) →
    block.DefUse (use.setValue ctx block' useInBounds) array := by
  intros hWF
  constructor <;> grind

@[grind .]
theorem BlockPtr.DefUse.OpOperandPtr_setValue_other_of_value_ne
    {ctx : IRContext OpInfo} {use : BlockOperandPtr} {useInBounds} (block : BlockPtr)
    (useOfOtherValue' : (use.get! ctx).value ≠ block')
    (valueNe : block ≠ block') {array} :
    block'.DefUse ctx array missingUses →
    block'.DefUse (use.setValue ctx block useInBounds) array missingUses := by
  intro hWF
  apply BlockPtr.DefUse.unchanged (ctx := ctx) <;> grind

end BlockPtr.DefUse

/--
  An operation chain owned by a block.
  An operation chain is a doubly linked list of operations within a block, where each
  operation points to the next and previous operations in the block. The block itself
  points to the first and last operations in the chain.
  The operation chain is represented as an ordered array of operation pointers, where
  the first element of the array is the first operation in the block, and the last
  element is the last operation in the block.
  Each operation that has the block as its parent must be included in the operation chain,
  unless it is included in the `missingOps` set.
-/
structure BlockPtr.OpChain (block : BlockPtr) (ctx : IRContext OpInfo) (array : Array OperationPtr)
    (missingOps : Std.ExtHashSet OperationPtr := ∅) : Prop where
  blockInBounds : block.InBounds ctx
  arrayInBounds (h : op ∈ array) : op.InBounds ctx
  missingOpInBounds (hin : op ∈ missingOps) : op.InBounds ctx
  opParent (h : op ∈ array) : (op.get! ctx).parent = some block
  missingOpValue (hin : op ∈ missingOps) : (op.get! ctx).parent = block
  allOpsInChain (op : OperationPtr) (opInBounds : op.InBounds ctx) :
    (op.get! ctx).parent = some block → (op ∈ array ↔ op ∉ missingOps)
  first : (block.get! ctx).firstOp = array[0]?
  last : (block.get! ctx).lastOp = array[array.size-1]?
  prevFirst (h : (block.get! ctx).firstOp = some firstOp) :
    (firstOp.get! ctx).prev = none
  prev i (h₁: i > 0) (h₂ : i < array.size) :
    (array[i].get! ctx).prev = some array[i - 1]
  next (hi : i < array.size) :
    (array[i].get! ctx).next = array[i + 1]?


attribute [grind →] BlockPtr.OpChain.blockInBounds

@[grind .]
theorem BlockPtr.OpChain_unique :
    BlockPtr.OpChain block ctx array →
    BlockPtr.OpChain block ctx array' →
    array = array' := by
  intros hWf hWf'
  apply Array.ext_getElem?
  intros i
  induction i <;> grind [BlockPtr.OpChain]

theorem BlockPtr.OpChain.firstOp_eq_none_iff_lastOp_eq_none :
    BlockPtr.OpChain block ctx array missingOps →
    ((block.get! ctx).firstOp = none ↔ (block.get! ctx).lastOp = none) := by
  grind [BlockPtr.OpChain]

theorem BlockPtr.OpChain.prev!_eq_none_iff_firstOp!_eq_self {op : OperationPtr}
    (hopInBounds : op.InBounds ctx)
    (hchain : BlockPtr.OpChain block ctx array)
    (hop : (op.get! ctx).parent = some block) :
    ((op.get! ctx).prev = none ↔ (block.get! ctx).firstOp = some op) := by
  constructor
  · intro hprev
    have opInArray : op ∈ array := by grind [BlockPtr.OpChain]
    have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem opInArray
    have : i = 0 := by grind [BlockPtr.OpChain]
    grind [BlockPtr.OpChain]
  · grind [BlockPtr.OpChain]

theorem BlockPtr.OpChain.next!_eq_none_iff_lastOp!_eq_self {op : OperationPtr}
    (hopInBounds : op.InBounds ctx)
    (hchain : BlockPtr.OpChain block ctx array)
    (hop : (op.get! ctx).parent = some block) :
    ((op.get! ctx).next = none ↔ (block.get! ctx).lastOp = some op) := by
  constructor
  · intro hprev
    have opInArray : op ∈ array := by grind [BlockPtr.OpChain]
    have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem opInArray
    have : i = array.size - 1 := by grind [BlockPtr.OpChain]
    grind [BlockPtr.OpChain]
  · grind [BlockPtr.OpChain]

@[grind .]
theorem BlockPtr.OpChain.parent!_firstOp_eq
    (hChain : BlockPtr.OpChain block ctx array missingOps)
    {firstOp : OperationPtr} :
    (block.get! ctx).firstOp = some firstOp →
    (firstOp.get! ctx).parent = some block := by
  grind [BlockPtr.OpChain]

@[grind .]
theorem BlockPtr.OpChain.parent!_lastOp_eq
    (hChain : BlockPtr.OpChain block ctx array missingOps)
    {lastOp : OperationPtr} :
    (block.get! ctx).lastOp = some lastOp →
    (lastOp.get! ctx).parent = some block := by
  grind [BlockPtr.OpChain]

@[grind .]
theorem BlockPtr.OpChain.parent!_prevOp_eq
    {op prevOp : OperationPtr}
    (hChain : BlockPtr.OpChain block ctx array)
    (opInBounds : op.InBounds ctx) :
    (op.get! ctx).parent = some block →
    (op.get! ctx).prev = some prevOp →
    (prevOp.get! ctx).parent = some block := by
  intros hParent hPrev
  have : op ∈ array := by grind [BlockPtr.OpChain]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  cases i <;> grind [BlockPtr.OpChain]

@[grind .]
theorem BlockPtr.OpChain.parent!_nextOp_eq
    {op nextOp : OperationPtr}
    (hChain : BlockPtr.OpChain block ctx array)
    (opInBounds : op.InBounds ctx) :
    (op.get! ctx).parent = some block →
    (op.get! ctx).next = some nextOp →
    (nextOp.get! ctx).parent = some block := by
  intros hParent hPext
  have : op ∈ array := by grind [BlockPtr.OpChain]
  have ⟨i, iInBounds, hi⟩ := Array.getElem_of_mem this
  cases i <;> grind [BlockPtr.OpChain]

structure RegionPtr.BlockChain (region : RegionPtr) (ctx : IRContext OpInfo) (array : Array BlockPtr) : Prop where
  inBounds : region.InBounds ctx
  arrayInBounds (h : bl ∈ array) : bl.InBounds ctx
  opParent (h : bl ∈ array) : (bl.get! ctx).parent = some region
  first : (region.get! ctx).firstBlock = array[0]?
  last : (region.get! ctx).lastBlock = array[array.size-1]?
  prevFirst (h : (region.get! ctx).firstBlock = some fbl) :
    (fbl.get! ctx).prev = none
  prev i (h₁: i > 0) (h₂ : i < array.size) :
    (array[i].get! ctx).prev = some array[i - 1]
  next (hi : i < array.size) :
    (array[i].get! ctx).next = array[i + 1]?
  allBlocksInChain (bl : BlockPtr) (blInBoundsl : bl.InBounds ctx) :
    (bl.get! ctx).parent = some region → bl ∈ array

attribute [grind →] RegionPtr.BlockChain.inBounds

@[grind .]
theorem RegionPtr.BlockChain_unique :
    RegionPtr.BlockChain region ctx array →
    RegionPtr.BlockChain region ctx array' →
    array = array' := by
  intros hWf hWf'
  apply Array.ext_getElem?
  intros i
  induction i <;> grind [RegionPtr.BlockChain]

theorem RegionPtr.BlockChain_array_injective
    (hWF : RegionPtr.BlockChain region ctx array) :
    ∀ (i j : Nat) iInBounds jInBounds, i ≠ j → array[i]'iInBounds ≠ array[j]'jInBounds := by
  intros i
  induction i
  case zero => grind [RegionPtr.BlockChain]
  case succ i ih =>
    intros j
    cases j
    case zero => grind [RegionPtr.BlockChain]
    case succ j =>
      intros iInBounds jInBounds hNe
      grind [RegionPtr.BlockChain]

structure OperationPtr.WellFormed (ctx : IRContext OpInfo) (opPtr : OperationPtr) hop : Prop where
  inBounds : Operation.FieldsInBounds opPtr ctx hop
  result_index i (iInBounds : i < opPtr.getNumResults! ctx) : ((opPtr.getResult i).get! ctx).index = i
  result_owner i (iInBounds : i < opPtr.getNumResults! ctx) :
    ((opPtr.getResult i).get! ctx).owner = opPtr
  operand_owner i (iInBounds : i < opPtr.getNumOperands! ctx) : ((opPtr.getOpOperand i).get! ctx).owner = opPtr
  blockOperand_owner i (iInBounds : i < opPtr.getNumSuccessors! ctx) : ((opPtr.getBlockOperand i).get! ctx).owner = opPtr
  regions_unique i (iInBounds : i < opPtr.getNumRegions! ctx) j (jInBounds : j < opPtr.getNumRegions! ctx) :
    i ≠ j → opPtr.getRegion ctx i ≠ opPtr.getRegion ctx j
  region_parent region (regionInBounds : region.InBounds ctx) :
    (∃ i, i < opPtr.getNumRegions! ctx ∧ opPtr.getRegion! ctx i = region) ↔
    (region.get! ctx).parent = some opPtr
  opChain_of_parent_none : (opPtr.get! ctx).parent = none →
    (opPtr.get! ctx).prev = none ∧ (opPtr.get! ctx).next = none

structure BlockPtr.WellFormed (ctx : IRContext OpInfo) (blockPtr : BlockPtr) hbl : Prop where
  inBounds : Block.FieldsInBounds blockPtr ctx hbl
  argument i (iInBounds : i < blockPtr.getNumArguments! ctx) : ((blockPtr.getArgument i).get! ctx).index = i
  argument_owners i (iInBounds : i < blockPtr.getNumArguments! ctx) : ((blockPtr.getArgument i).get! ctx).owner = blockPtr
  prev_eq_of_parent_eq_none : (blockPtr.get! ctx).parent = none →
    (blockPtr.get! ctx).prev = none
  next_eq_of_parent_eq_none : (blockPtr.get! ctx).parent = none →
    (blockPtr.get! ctx).next = none

structure RegionPtr.WellFormed (ctx : IRContext OpInfo) (regionPtr : RegionPtr) where
  inBounds : (regionPtr.get! ctx).FieldsInBounds ctx
  parent_op {op} (heq : (regionPtr.get! ctx).parent = some op) : ∃ i, i < op.getNumRegions! ctx ∧ op.getRegion! ctx i = regionPtr

structure IRContext.WellFormed (ctx : IRContext OpInfo)
  (missingOperandUses : Std.ExtHashSet OpOperandPtr := ∅)
  (missingSuccessorUses : Std.ExtHashSet BlockOperandPtr := ∅) : Prop where
  inBounds : ctx.FieldsInBounds
  valueDefUseChains (valuePtr : ValuePtr) (valuePtrInBounds : valuePtr.InBounds ctx) :
    ∃ array, ValuePtr.DefUse valuePtr ctx array (missingOperandUses.filter (fun use => (use.get! ctx).value = valuePtr))
  blockDefUseChains (blockPtr : BlockPtr) (blockPtrInBounds : blockPtr.InBounds ctx) :
    ∃ array, BlockPtr.DefUse blockPtr ctx array (missingSuccessorUses.filter (fun use => (use.get! ctx).value = blockPtr))
  opChain (blockPtr : BlockPtr) (blockPtrInBounds : blockPtr.InBounds ctx) :
    ∃ array, BlockPtr.OpChain blockPtr ctx array
  blockChain (regionPtr : RegionPtr) (regionPtrInBounds : regionPtr.InBounds ctx) :
    ∃ array, RegionPtr.BlockChain regionPtr ctx array
  operations (opPtr : OperationPtr) (opPtrInBounds : opPtr.InBounds ctx) :
    opPtr.WellFormed ctx opPtrInBounds
  blocks (blockPtr : BlockPtr) (blockPtrInBounds : blockPtr.InBounds ctx) :
    blockPtr.WellFormed ctx blockPtrInBounds
  regions (regionPtr : RegionPtr) (regionPtrInBounds : regionPtr.InBounds ctx) :
    regionPtr.WellFormed ctx

attribute [grind →] IRContext.WellFormed.inBounds

@[grind .]
theorem IRContext.empty_wellFormed [HasOpInfo opInfo] :
    (IRContext.empty opInfo).WellFormed := by
  grind [IRContext.WellFormed]

theorem BlockPtr.OpChain_unchanged
    (hWf : blockPtr.OpChain ctx array missingOps)
    (blockPtrInBounds' : blockPtr.InBounds ctx')
    (hSameFirstOp : (blockPtr.get! ctx).firstOp = (blockPtr.get! ctx').firstOp)
    (hSameLastOp : (blockPtr.get! ctx).lastOp = (blockPtr.get! ctx').lastOp)
    (hSameOpFields : ∀ (opPtr : OperationPtr),
      opPtr.InBounds ctx →
      (opPtr.get! ctx).parent = some blockPtr →
        opPtr.InBounds ctx' ∧
        (opPtr.get! ctx').parent = (opPtr.get! ctx).parent ∧
        (opPtr.get! ctx').prev = (opPtr.get! ctx).prev ∧
        (opPtr.get! ctx').next = (opPtr.get! ctx).next)
    (hSameOpFields' : ∀ (opPtr : OperationPtr),
      opPtr.InBounds ctx' →
      (opPtr.get! ctx').parent = some blockPtr →
        opPtr.InBounds ctx ∧
        (opPtr.get! ctx).parent = (opPtr.get! ctx').parent) :
    blockPtr.OpChain ctx' array missingOps := by
  constructor <;> grind [BlockPtr.OpChain]

theorem BlockPtr.OpChain_array_injective
    (hWF : BlockPtr.OpChain block ctx array missingOps) :
    ∀ (i j : Nat) iInBounds jInBounds, i ≠ j → array[i]'iInBounds ≠ array[j]'jInBounds := by
  intros i
  induction i
  case zero => grind [BlockPtr.OpChain]
  case succ i ih =>
    intros j
    cases j
    case zero => grind [BlockPtr.OpChain]
    case succ j =>
      intros iInBounds jInBounds hNe
      grind [BlockPtr.OpChain]

theorem BlockPtr.OpChain_array_toList_Nodup
    (hWF : BlockPtr.OpChain block ctx array missingOps) :
    array.toList.Nodup := by
  simp only [List.nodup_iff_pairwise_ne]
  simp only [List.pairwise_iff_getElem]
  grind [BlockPtr.OpChain_array_injective]

@[grind .]
theorem BlockPtr.OpChain.array_mem_erase
    (hWF : BlockPtr.OpChain block ctx array missingOps) :
    op ∈ array.erase op' ↔ op ∈ array ∧ op ≠ op' := by
  have := BlockPtr.OpChain_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind [List.Nodup.not_mem_erase]

@[grind .]
theorem BlockPtr.OpChain.idxOf_getElem_array
    (hWF : BlockPtr.OpChain block ctx array missingOps) :
    ∀ (i : Nat) (iInBounds : i < array.size),
    array.idxOf array[i] = i := by
  have := BlockPtr.OpChain_array_toList_Nodup hWF
  rw [← Array.toArray_toList (xs := array)]
  grind  [List.idxOf_getElem]

@[grind .]
theorem BlockPtr.OpChain.erase_getElem_array_eq_eraseIdx
    (hWF : BlockPtr.OpChain block ctx array missingOps) :
    (array.erase (array[i]'iInBounds)) = array.eraseIdx i iInBounds := by
  grind [Array.erase_eq_eraseIdx_of_idxOf, BlockPtr.OpChain.idxOf_getElem_array]

theorem RegionPtr.blockChain_unchanged
    (hWf : regionPtr.BlockChain ctx array)
    (regionPtrInBounds' : regionPtr.InBounds ctx')
    (hSameFirst : (regionPtr.get! ctx).firstBlock = (regionPtr.get! ctx').firstBlock)
    (hSameLast : (regionPtr.get! ctx).lastBlock = (regionPtr.get! ctx').lastBlock)
    (hSameBlockFields : ∀ (blockPtr : BlockPtr),
      blockPtr.InBounds ctx →
      (blockPtr.get! ctx).parent = some regionPtr →
        blockPtr.InBounds ctx' ∧
        (blockPtr.get! ctx').parent = (blockPtr.get! ctx).parent ∧
        (blockPtr.get! ctx').prev = (blockPtr.get! ctx).prev ∧
        (blockPtr.get! ctx').next = (blockPtr.get! ctx).next)
    (hSameBlockFields' : ∀ (blockPtr : BlockPtr),
      blockPtr.InBounds ctx' →
      (blockPtr.get! ctx').parent = some regionPtr →
        blockPtr.InBounds ctx ∧
        (blockPtr.get! ctx).parent = (blockPtr.get! ctx').parent) :
    regionPtr.BlockChain ctx' array := by
  constructor <;> grind [RegionPtr.BlockChain]

theorem OperationPtr.WellFormed_unchanged
    (hWf : opPtr.WellFormed ctx opPtrInBounds)
    (hInBounds' : Operation.FieldsInBounds opPtr ctx' opPtrInBounds')
    (hSameNumOperands :
      opPtr.getNumOperands! ctx = opPtr.getNumOperands! ctx')
    (hSameOperandOwner :
      ∀ i, i < opPtr.getNumOperands! ctx →
      ((opPtr.getOpOperand i).get! ctx).owner = ((opPtr.getOpOperand i).get! ctx').owner)
    (hSameNumBlockOperands :
      opPtr.getNumSuccessors! ctx = opPtr.getNumSuccessors! ctx')
    (hSameBlockOperandOwner :
      ∀ i, i < opPtr.getNumSuccessors! ctx →
      ((opPtr.getBlockOperand i).get! ctx).owner = ((opPtr.getBlockOperand i).get! ctx').owner)
    (hSameNumResults :
      opPtr.getNumResults! ctx = opPtr.getNumResults! ctx')
    (hSameResultIndex :
      ∀ i, i < opPtr.getNumResults ctx opPtrInBounds →
      ((opPtr.getResult i).get! ctx).index = ((opPtr.getResult i).get! ctx').index)
    (hSameResultOwner :
      ∀ i, i < opPtr.getNumResults ctx opPtrInBounds →
      ((opPtr.getResult i).get! ctx).owner = ((opPtr.getResult i).get! ctx').owner)
    (hSameParent : (opPtr.get! ctx).parent = (opPtr.get! ctx').parent)
    (hSamePrev : (opPtr.get! ctx).prev = (opPtr.get! ctx').prev)
    (hSameNext : (opPtr.get! ctx).next = (opPtr.get! ctx').next)
    (hSameRegionParents :
      ∀ (regionPtr : RegionPtr), regionPtr.InBounds ctx →
        (regionPtr.get! ctx).parent = some opPtr →
        regionPtr.InBounds ctx' ∧ (regionPtr.get! ctx).parent = (regionPtr.get! ctx').parent)
    (hSameRegionParents' :
      ∀ (regionPtr : RegionPtr), regionPtr.InBounds ctx' →
        (regionPtr.get! ctx').parent = some opPtr →
        regionPtr.InBounds ctx ∧ (regionPtr.get! ctx).parent = (regionPtr.get! ctx').parent)
    (hSameNumRegions :
      opPtr.getNumRegions! ctx = opPtr.getNumRegions! ctx')
    (hSameRegions :
      ∀ i, i < opPtr.getNumRegions! ctx → opPtr.getRegion! ctx i = opPtr.getRegion! ctx' i) :
    opPtr.WellFormed ctx' opPtrInBounds' := by
  constructor <;> grind [OperationPtr.WellFormed, Operation.FieldsInBounds]

theorem BlockPtr.WellFormed_unchanged
    (hWf : blockPtr.WellFormed ctx blockPtrInBounds)
    (hInBounds' : Block.FieldsInBounds blockPtr ctx' blockPtrInBounds')
    (hSameParent : (blockPtr.get! ctx).parent = (blockPtr.get! ctx').parent)
    (hSamePrev : (blockPtr.get! ctx).prev = (blockPtr.get! ctx').prev)
    (hSameNext : (blockPtr.get! ctx).next = (blockPtr.get! ctx').next)
    (hSameNumArguments : blockPtr.getNumArguments! ctx = blockPtr.getNumArguments! ctx')
    (hSameArgumentOwner :
      ∀ i, i < blockPtr.getNumArguments! ctx →
      ((blockPtr.getArgument i).get! ctx).owner = ((blockPtr.getArgument i).get! ctx').owner)
    (hSameArgumentIndex :
      ∀ i, i < blockPtr.getNumArguments ctx →
      ((blockPtr.getArgument i).get! ctx).index = ((blockPtr.getArgument i).get! ctx').index) :
    blockPtr.WellFormed ctx' blockPtrInBounds' := by
  constructor <;> grind [BlockPtr.WellFormed]

theorem RegionPtr.WellFormed_unchanged {regionPtr : RegionPtr}
    (hWf : regionPtr.WellFormed ctx)
    (hInBounds' : (regionPtr.get! ctx').FieldsInBounds ctx')
    (hSameParentOp : (regionPtr.get! ctx).parent = (regionPtr.get! ctx').parent)
    (hSameNumRegions :
      ∀ parent, (regionPtr.get! ctx).parent = some parent →
      parent.getNumRegions! ctx = parent.getNumRegions! ctx')
    (hSameRegions :
      ∀ parent, (regionPtr.get! ctx).parent = some parent →
      ∀ i, i < parent.getNumRegions! ctx →
      parent.getRegion! ctx i = parent.getRegion! ctx' i) :
    regionPtr.WellFormed ctx' := by
  constructor <;> grind [RegionPtr.WellFormed]

noncomputable def BlockPtr.operationList (block : BlockPtr) (ctx : IRContext OpInfo)
    (hctx : ctx.WellFormed := by grind) (hblock : block.InBounds ctx := by grind) :
    Array OperationPtr :=
  (hctx.opChain block hblock).choose

theorem BlockPtr.operationListWF (ctx : IRContext OpInfo) (block : BlockPtr) (hblock : block.InBounds ctx)
  (hctx : ctx.WellFormed) :
    BlockPtr.OpChain block ctx (BlockPtr.operationList block ctx hctx hblock) :=
  Exists.choose_spec (hctx.opChain block hblock)

@[grind =]
theorem BlockPtr.operationList_iff_BlockPtr_OpChain :
    BlockPtr.OpChain block ctx array ↔
    BlockPtr.operationList block ctx hctx hblock = array := by
  grind [BlockPtr.operationListWF]

@[grind =_]
theorem BlockPtr.operationList.mem (h : op.InBounds ctx) :
    (op.get! ctx).parent = some block ↔
    op ∈ BlockPtr.operationList block ctx hctx hblock := by
  grind [BlockPtr.OpChain, BlockPtr.operationListWF]

noncomputable def RegionPtr.blockList (region : RegionPtr)
    (ctx : IRContext OpInfo) (hctx : ctx.WellFormed := by grind)
    (hregion : region.InBounds ctx := by grind) : Array BlockPtr :=
  (hctx.blockChain region hregion).choose

@[grind .]
theorem RegionPtr.blockListWF (ctx : IRContext OpInfo) (region : RegionPtr)
    (hregion : region.InBounds ctx := by grind)
    (hctx : ctx.WellFormed := by grind) :
    RegionPtr.BlockChain region ctx (RegionPtr.blockList region ctx hctx hregion) :=
  Exists.choose_spec (hctx.blockChain region hregion)

@[grind =]
theorem RegionPtr.blockList_iff_RegionPtr_BlockChain :
    RegionPtr.BlockChain region ctx array ↔
    RegionPtr.blockList region ctx hctx hregion = array := by
  grind [RegionPtr.blockListWF]

@[grind =_]
theorem RegionPtr.blockList.mem :
    (bl.get ctx blInBounds).parent = some region ↔
    bl ∈ RegionPtr.blockList region ctx hctx hregion := by
  grind [RegionPtr.BlockChain, RegionPtr.blockListWF]

noncomputable def ValuePtr.defUseArray (value : ValuePtr) (ctx : IRContext OpInfo) (hctx : ctx.WellFormed missingUses missingBlockUses) (hvalue : value.InBounds ctx) : Array OpOperandPtr :=
  (hctx.valueDefUseChains value hvalue).choose

@[grind .]
theorem ValuePtr.defUseArrayWF {hctx : IRContext.WellFormed ctx missingUses missingBlockUses} :
    ValuePtr.DefUse value ctx (ValuePtr.defUseArray value ctx hctx hvalue) (missingUses.filter (fun use => (use.get! ctx).value = value)) := by
  grind [ValuePtr.defUseArray, IRContext.WellFormed]

@[grind .]
theorem ValuePtr.defUseArray_iff_ValuePtr_DefUse {hctx : ctx.WellFormed missingUses missingBlockUses} :
    ValuePtr.DefUse value ctx array (missingUses.filter (fun use => (use.get! ctx).value = value)) ↔
    ValuePtr.defUseArray value ctx hctx hvalue = array := by
  grind [ValuePtr.defUseArrayWF]

theorem ValuePtr.defUseArray_contains_operand_use
{hctx : IRContext.WellFormed ctx} (h : operand.InBounds ctx) :
    (operand.get! ctx).value = value ↔
    operand ∈ ValuePtr.defUseArray value ctx hctx hvalue := by
  grind [ValuePtr.DefUse, ValuePtr.defUseArrayWF]

theorem OperationPtr.getParent_prev_eq
    (opInBounds : OperationPtr.InBounds opPtr ctx)
    (hopParent : (OperationPtr.get! opPtr ctx).parent = some block)
    (hblock : BlockPtr.OpChain block ctx array)
    (hprev : (OperationPtr.get! opPtr ctx).prev = some prevOp) :
    (prevOp.get! ctx).parent = some block := by
  grind [BlockPtr.OpChain, Array.getElem?_of_mem]

theorem BlockPtr.OpChain_prev_ne
    (hop : OperationPtr.InBounds op ctx)
    (hctx : ctx.WellFormed)
    (hparent : (op.get! ctx).parent = some block) :
    block.OpChain ctx array →
    (op.get! ctx).prev ≠ some op := by
  intros hNe
  have := hctx.inBounds
  have ⟨array, harray⟩ := hctx.opChain block (by grind)
  have : op ∈ array := by grind [BlockPtr.OpChain]
  intro heq
  have ⟨i, hi⟩ := Array.getElem_of_mem this
  have : array[i]'(by grind) = op := by grind
  have : i > 0 := by grind [BlockPtr.OpChain]
  have := harray.prev i (by grind) (by grind)
  have : op = array[i - 1]'(by grind) := by grind
  grind [BlockPtr.OpChain_array_injective]

theorem BlockPtr.OpChain_next_ne
    (hop : OperationPtr.InBounds op ctx) (hctx : ctx.WellFormed)
    (hparent : (op.get! ctx).parent = some block) :
    block.OpChain ctx array →
    (op.get! ctx).next ≠ some op := by
  intros hNe
  have := hctx.inBounds
  have ⟨array, harray⟩ := hctx.opChain block (by grind)
  have : op ∈ array := by grind [BlockPtr.OpChain]
  intro heq
  have ⟨i, hi⟩ := Array.getElem?_of_mem this
  have : array[i + 1]? = some op := by grind [BlockPtr.OpChain]
  grind [BlockPtr.OpChain_array_injective]

theorem ValuePtr.DefUse.hasUses!_iff
    (hWF : ValuePtr.DefUse value ctx array missingUses) :
    value.hasUses! ctx ↔ array ≠ #[] := by
  grind [DefUse, hasUses!_def]

theorem ValuePtr.DefUse.getFirstUse!_none_iff
    (hWF : ValuePtr.DefUse value ctx array missingUses) :
    value.getFirstUse! ctx = none ↔ array = #[] := by
  grind [DefUse]

theorem IRContext.WellFormed.OperationPtr_next!_eq_some_of_prev!_eq_some
    {ctx : IRContext OpInfo} {op prevOp : OperationPtr} (hop : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (op.get! ctx).prev = some prevOp →
    (prevOp.get! ctx).next = some op := by
  cases hparent : (op.get! ctx).parent
  case none =>
    grind [IRContext.WellFormed, BlockPtr.OpChain, OperationPtr.WellFormed]
  case some parent =>
    intro hprev
    have ⟨array, harray⟩ := wf.opChain parent (by grind)
    grind [Array.getElem?_of_mem, BlockPtr.OpChain]

theorem IRContext.WellFormed.OperationPtr_prev!_eq_some_of_next!_eq_some
    {ctx : IRContext OpInfo} {op nextOp : OperationPtr} (hop : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (op.get! ctx).next = some nextOp →
    (nextOp.get! ctx).prev = some op := by
  cases hparent : (op.get! ctx).parent
  case none =>
    grind [IRContext.WellFormed, BlockPtr.OpChain, OperationPtr.WellFormed]
  case some parent =>
    intro hprev
    have ⟨array, harray⟩ := wf.opChain parent (by grind)
    grind [Array.getElem?_of_mem, BlockPtr.OpChain]

theorem IRContext.WellFormed.OperationPtr_parent!_ne_none_of_next!_ne_none
    {ctx : IRContext OpInfo} {op : OperationPtr} (hop : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (op.get! ctx).next ≠ none →
    (op.get! ctx).parent ≠ none := by
  grind [IRContext.WellFormed, OperationPtr.WellFormed]

grind_pattern IRContext.WellFormed.OperationPtr_parent!_ne_none_of_next!_ne_none =>
  ctx.WellFormed missingUses missingSuccessorUses, (op.get! ctx).next, (op.get! ctx).parent

theorem IRContext.WellFormed.OperationPtr_parent!_ne_none_of_prev!_ne_none
    {ctx : IRContext OpInfo} {op : OperationPtr} (hop : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (op.get! ctx).prev ≠ none →
    (op.get! ctx).parent ≠ none := by
  grind [IRContext.WellFormed, OperationPtr.WellFormed]

grind_pattern IRContext.WellFormed.OperationPtr_parent!_ne_none_of_prev!_ne_none =>
  ctx.WellFormed missingUses missingSuccessorUses, (op.get! ctx).prev, (op.get! ctx).parent

theorem IRContext.WellFormed.OpOperandPtr_value!_eq_of_back!_eq_valueFirstUse
    {ctx : IRContext OpInfo} (wf : ctx.WellFormed)
    {firstUse : OpOperandPtr} (firstUseInBounds : firstUse.InBounds ctx) :
    (firstUse.get! ctx).back = .valueFirstUse value →
    (firstUse.get! ctx).value = value := by
  have ⟨array, harray⟩ := wf.valueDefUseChains (firstUse.get! ctx).value (by grind)
  have hMem : firstUse ∈ array := by grind [ValuePtr.DefUse]
  grind [ValuePtr.DefUse.value!_eq_of_back!_eq_valueFirstUse]

grind_pattern IRContext.WellFormed.OpOperandPtr_value!_eq_of_back!_eq_valueFirstUse =>
  ctx.WellFormed, (firstUse.get! ctx).back, OpOperandPtrPtr.valueFirstUse value

theorem IRContext.WellFormed.OpOperandPtr_value_of_getFirstUse (wf : ctx.WellFormed)
    (valueInBounds : value.InBounds ctx) {firstUse : OpOperandPtr}
    (hFirstUse : value.getFirstUse! ctx = some firstUse) :
    (firstUse.get! ctx).value = value := by
  have ⟨array, harray⟩ := wf.valueDefUseChains value (by grind)
  grind [ValuePtr.DefUse]

grind_pattern IRContext.WellFormed.OpOperandPtr_value_of_getFirstUse =>
  ctx.WellFormed, value.getFirstUse! ctx, some firstUse

theorem IRContext.WellFormed.ValuePtr_getFirstUse!_eq_of_back_eq_valueFirstUse
    {ctx : IRContext OpInfo} (wf : ctx.WellFormed) {firstUse : OpOperandPtr}
    (firstUseInBounds : firstUse.InBounds ctx)
    (heq : (firstUse.get! ctx).back = .valueFirstUse value) :
    value.getFirstUse! ctx = some firstUse := by
  have ⟨array, harray⟩ := wf.valueDefUseChains value (by grind)
  have := @ValuePtr.DefUse.getFirstUse!_eq_of_back_eq_valueFirstUse
  grind [IRContext.WellFormed.OpOperandPtr_value!_eq_of_back!_eq_valueFirstUse, ValuePtr.DefUse]

grind_pattern IRContext.WellFormed.ValuePtr_getFirstUse!_eq_of_back_eq_valueFirstUse =>
  ctx.WellFormed, (firstUse.get! ctx).back, OpOperandPtrPtr.valueFirstUse value

theorem IRContext.WellFormed.ValuePtr_hasUses_iff_operand_value_ne_value
    (ctxWf : ctx.WellFormed)
    {value : ValuePtr} (noUses : ¬ value.hasUses! ctx) (valueInBounds : value.InBounds ctx)
    {operand : OpOperandPtr} (hoperand : operand.InBounds ctx) :
    (operand.get! ctx).value ≠ value := by
  grind [valueDefUseChains, ValuePtr.DefUse.hasUses!_iff, ValuePtr.DefUse]

grind_pattern IRContext.WellFormed.ValuePtr_hasUses_iff_operand_value_ne_value =>
  ctx.WellFormed, value.hasUses! ctx, (operand.get! ctx).value

theorem BlockPtr.OpChain.mem_next!_of_mem
    (op nextOp : OperationPtr) (block : BlockPtr)
    (hWF : block.OpChain ctx array missingOps)
    (hnext : (op.get! ctx).next = some nextOp)
    (hmem : op ∈ array) :
    nextOp ∈ array := by
  grind [BlockPtr.OpChain, Array.getElem_of_mem]

grind_pattern BlockPtr.OpChain.mem_next!_of_mem =>
  block.OpChain ctx array missingOps, (op.get! ctx).next, some nextOp, op ∈ array

theorem BlockPtr.OpChain.mem_of_mem_next!
    (op nextOp : OperationPtr) (block : BlockPtr)
    (ctxWF : ctx.WellFormed)
    (hWF : block.OpChain ctx array)
    (hnext : (op.get! ctx).next = some nextOp)
    (hop : op.InBounds ctx)
    (hmem : nextOp ∈ array) :
    op ∈ array := by
  cases hparent : (op.get! ctx).parent; grind
  rename_i block'
  have ⟨array', harray'⟩ := ctxWF.opChain block' (by grind)
  grind

grind_pattern BlockPtr.OpChain.mem_of_mem_next! =>
  ctx.WellFormed, block.OpChain ctx array, (op.get! ctx).next, some nextOp, nextOp ∈ array

@[grind <=]
theorem OperationPtr.parent!_next
    (op nextOp : OperationPtr)
    (hWF : ctx.WellFormed)
    (opInBounds : op.InBounds ctx)
    (hnext : (op.get! ctx).next = some nextOp)
    (hparent : (nextOp.get! ctx).parent = some block) :
    (op.get! ctx).parent = some block := by
  have ⟨array, harray⟩ := hWF.opChain block (by grind)
  have : nextOp ∈ array := by grind
  grind

theorem IRContext.WellFormed.BlockPtr_next!_eq_some_of_prev!_eq_some
    {ctx : IRContext OpInfo} {bl prevBl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (bl.get! ctx).prev = some prevBl →
    (prevBl.get! ctx).next = some bl := by
  cases hparent : (bl.get! ctx).parent
  case none =>
    grind [IRContext.WellFormed, RegionPtr.BlockChain, BlockPtr.WellFormed]
  case some parent =>
    intro hprev
    have ⟨array, harray⟩ := wf.blockChain parent (by grind)
    grind [Array.getElem?_of_mem, RegionPtr.BlockChain]

grind_pattern IRContext.WellFormed.BlockPtr_next!_eq_some_of_prev!_eq_some =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).prev, some prevBl, (prevBl.get! ctx).next

theorem IRContext.WellFormed.BlockPtr_prev!_eq_some_of_next!_eq_some
    {ctx : IRContext OpInfo} {bl nextBl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (bl.get! ctx).next = some nextBl →
    (nextBl.get! ctx).prev = some bl := by
  cases hparent : (bl.get! ctx).parent
  case none =>
    grind [IRContext.WellFormed, RegionPtr.BlockChain, BlockPtr.WellFormed]
  case some parent =>
    intro hnext
    have ⟨array, harray⟩ := wf.blockChain parent (by grind)
    grind [Array.getElem?_of_mem, RegionPtr.BlockChain]

grind_pattern IRContext.WellFormed.BlockPtr_prev!_eq_some_of_next!_eq_some =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).next, some nextBl, (nextBl.get! ctx).prev

theorem IRContext.WellFormed.BlockPtr_parent!_ne_none_of_next!_ne_none
    {bl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (bl.get! ctx).next ≠ none →
    (bl.get! ctx).parent ≠ none := by
  grind [IRContext.WellFormed, BlockPtr.WellFormed]

grind_pattern IRContext.WellFormed.BlockPtr_parent!_ne_none_of_next!_ne_none =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).next, (bl.get! ctx).parent

theorem IRContext.WellFormed.BlockPtr_parent!_ne_none_of_prev!_ne_none
    {bl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (bl.get! ctx).prev ≠ none →
    (bl.get! ctx).parent ≠ none := by
  grind [IRContext.WellFormed, BlockPtr.WellFormed]

grind_pattern IRContext.WellFormed.BlockPtr_parent!_ne_none_of_prev!_ne_none =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).prev, (bl.get! ctx).parent

@[grind <=]
theorem IRContext.WellFormed.exists_parent!_eq_some_of_next!_eq_some
    {bl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses)
    (hnext : (bl.get! ctx).next = some nextBl) :
    ∃ parent, (bl.get! ctx).parent = some parent := by
  have := IRContext.WellFormed.BlockPtr_parent!_ne_none_of_next!_ne_none hbl wf (by grind)
  have := (Option.ne_none_iff_exists.mp this)
  grind

@[grind <=]
theorem IRContext.WellFormed.exists_parent!_eq_some_of_prev!_eq_some
    {bl : BlockPtr} (hbl : bl.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses)
    (hprev : (bl.get! ctx).prev = some prevBl) :
    ∃ parent, (bl.get! ctx).parent = some parent := by
  have := IRContext.WellFormed.BlockPtr_parent!_ne_none_of_prev!_ne_none hbl wf (by grind)
  have := (Option.ne_none_iff_exists.mp this)
  grind

theorem IRContext.WellFormed.firstOp!_eq_some_iff
    {block : BlockPtr} (blockInBounds : block.InBounds ctx)
    {op : OperationPtr} (opInBounds : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (block.get! ctx).firstOp = some op ↔
    ((op.get! ctx).parent = some block ∧ (op.get! ctx).prev = none) := by
  constructor
  · grind [IRContext.WellFormed, BlockPtr.OpChain.prev!_eq_none_iff_firstOp!_eq_self]
  · have ⟨array, harray⟩ := wf.opChain block (by grind)
    grind [BlockPtr.OpChain.prev!_eq_none_iff_firstOp!_eq_self]

grind_pattern IRContext.WellFormed.firstOp!_eq_some_iff =>
  ctx.WellFormed missingUses missingSuccessorUses, (block.get! ctx).firstOp, some op

grind_pattern IRContext.WellFormed.firstOp!_eq_some_iff =>
  ctx.WellFormed missingUses missingSuccessorUses, (op.get! ctx).parent, some block,
  (op.get! ctx).prev

theorem IRContext.WellFormed.lastOp!_eq_some_iff
    {block : BlockPtr} (blockInBounds : block.InBounds ctx)
    {op : OperationPtr} (opInBounds : op.InBounds ctx)
    (wf : ctx.WellFormed missingUses missingSuccessorUses) :
    (block.get! ctx).lastOp = some op ↔
    ((op.get! ctx).parent = some block ∧ (op.get! ctx).next = none) := by
  constructor
  · grind [IRContext.WellFormed, BlockPtr.OpChain.next!_eq_none_iff_lastOp!_eq_self]
  · have ⟨array, harray⟩ := wf.opChain block (by grind)
    grind [BlockPtr.OpChain.next!_eq_none_iff_lastOp!_eq_self]

grind_pattern IRContext.WellFormed.lastOp!_eq_some_iff =>
  ctx.WellFormed missingUses missingSuccessorUses, (block.get! ctx).lastOp, some op

grind_pattern IRContext.WellFormed.lastOp!_eq_some_iff =>
  ctx.WellFormed missingUses missingSuccessorUses, (op.get! ctx).parent, some block,
  (op.get! ctx).prev

theorem RegionPtr.BlockChain.mem_next!_of_mem
    (bl nextBl : BlockPtr) (region : RegionPtr)
    (hWF : region.BlockChain ctx array)
    (hnext : (bl.get! ctx).next = some nextBl)
    (hmem : bl ∈ array) :
    nextBl ∈ array := by
  grind [RegionPtr.BlockChain, Array.getElem_of_mem]

grind_pattern RegionPtr.BlockChain.mem_next!_of_mem =>
  region.BlockChain ctx array, (bl.get! ctx).next, some nextBl, bl ∈ array

theorem RegionPtr.BlockChain.mem_prev!_of_mem
    (bl prevBl : BlockPtr) (region : RegionPtr)
    (hWF : region.BlockChain ctx array)
    (hprev : (bl.get! ctx).prev = some prevBl)
    (hmem : bl ∈ array) :
    prevBl ∈ array := by
  grind [RegionPtr.BlockChain, Array.getElem_of_mem]

grind_pattern RegionPtr.BlockChain.mem_prev!_of_mem =>
  region.BlockChain ctx array, (bl.get! ctx).prev, some prevBl, bl ∈ array

theorem RegionPtr.BlockChain.mem_of_mem_next!
    (bl nextBl : BlockPtr) (region : RegionPtr)
    (ctxWF : ctx.WellFormed)
    (hWF : region.BlockChain ctx array)
    (hnext : (bl.get! ctx).next = some nextBl)
    (hbl : bl.InBounds ctx)
    (hmem : nextBl ∈ array) :
    bl ∈ array := by
  cases hparent : (bl.get! ctx).parent; grind
  rename_i region'
  have : region = region' := by grind [RegionPtr.BlockChain, Array.getElem_of_mem]
  have ⟨array', harray'⟩ := ctxWF.blockChain region' (by grind)
  grind

grind_pattern RegionPtr.BlockChain.mem_of_mem_next! =>
  ctx.WellFormed, region.BlockChain ctx array, (bl.get! ctx).next, some nextBl, nextBl ∈ array

theorem BlockPtr.parent!_next {bl : BlockPtr}
    (blInBounds : bl.InBounds ctx) (hctx : ctx.WellFormed missingUses missingSuccessorUses)
    (hnext : (bl.get! ctx).next = some nextBl) :
    (bl.get! ctx).parent = (nextBl.get! ctx).parent := by
  intros
  have ⟨parent, hparent⟩ : ∃ region, (bl.get! ctx).parent = some region := by grind
  have ⟨array, harray⟩ := hctx.blockChain parent (by grind)
  have : bl ∈ array := by grind [RegionPtr.BlockChain]
  grind [RegionPtr.BlockChain]

grind_pattern BlockPtr.parent!_next =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).next, some nextBl

theorem BlockPtr.parent!_prev {bl : BlockPtr}
    (blInBounds : bl.InBounds ctx) (hctx : ctx.WellFormed missingUses missingSuccessorUses)
    (hprev : (bl.get! ctx).prev = some prevBl) :
    (bl.get! ctx).parent = (prevBl.get! ctx).parent := by
  intros
  have ⟨parent, hparent⟩ : ∃ region, (bl.get! ctx).parent = some region := by grind
  have ⟨array, harray⟩ := hctx.blockChain parent (by grind)
  have : bl ∈ array := by grind [RegionPtr.BlockChain]
  grind [RegionPtr.BlockChain]

grind_pattern BlockPtr.parent!_prev =>
  ctx.WellFormed missingUses missingSuccessorUses, (bl.get! ctx).prev, some prevBl

theorem RegionPtr.lastBlock!_parent! {reg : RegionPtr}
    (regInBounds : reg.InBounds ctx) (hctx : ctx.WellFormed missingUses missingSuccessorUses)
    (hlast : (reg.get! ctx).lastBlock = some lastBl) :
    (lastBl.get! ctx).parent = some reg := by
  have ⟨array, harray⟩ := hctx.blockChain reg (by grind)
  grind [RegionPtr.BlockChain]

grind_pattern RegionPtr.lastBlock!_parent! =>
  ctx.WellFormed missingUses missingSuccessorUses, (reg.get! ctx).lastBlock, some lastBl,
  (lastBl.get! ctx).parent

/--
  Compute the index of an operation in its parent's operations list.
  If the operation does not have a parent, return 0.
-/
noncomputable def OperationPtr.idxInParent (op : OperationPtr) (ctx : IRContext OpInfo)
    (hop : op.InBounds ctx := by grind)
    (hctx : ctx.WellFormed := by grind) : Nat :=
  match hparent : (op.get! ctx).parent with
  | some block => (block.operationList ctx hctx (by grind)).idxOf op
  | none       => 0

@[grind .]
theorem OperationPtr.idxInParent_lt_size_operationList
    (op : OperationPtr) (ctx : IRContext OpInfo) (block : BlockPtr)
    (hasParent : (op.get! ctx).parent = some block)
    (hop : op.InBounds ctx)
    (hctx : ctx.WellFormed) :
    op.idxInParent ctx hop hctx <
      (block.operationList ctx hctx (by grind)).size := by
  grind [OperationPtr.idxInParent]

theorem OperationPtr.idxInParent_next_eq
    (op : OperationPtr) (ctx : IRContext OpInfo) (nextOp : OperationPtr)
    (hnext : (op.get! ctx).next = some nextOp)
    (hnextOp : nextOp.InBounds ctx)
    (hop : op.InBounds ctx)
    (hctx : ctx.WellFormed) :
    nextOp.idxInParent ctx hnextOp hctx =
      op.idxInParent ctx hop hctx + 1 := by
  simp only [OperationPtr.idxInParent]
  split
  next block nextParent =>
    split
    next block' opParent =>
      have : block = block' := by grind
      subst block'
      have ⟨array, harray⟩ := hctx.opChain block (by grind)
      grind [BlockPtr.OpChain.idxOf_getElem_array, BlockPtr.OpChain]
    next opParent => grind
  next nextParent =>
    split
    next block opParent =>
      have ⟨array, harray⟩ := hctx.opChain block (by grind)
      grind
    next opParent => grind

grind_pattern OperationPtr.idxInParent_next_eq =>
  nextOp.idxInParent ctx hnextOp hctx, (op.get! ctx).next, some nextOp

/--
  Compute the index of an operation in its parent's operations list from the tail
  (i.e. the last operation has index 0).
  If the operation does not have a parent, return 0.

  This function is useful for proving termination of recursive functions that traverse
  the operation list, as this function decreases when we move to the next operation in the list.
-/
noncomputable def OperationPtr.idxInParentFromTail (op : OperationPtr) (ctx : IRContext OpInfo)
    (hop : op.InBounds ctx := by grind)
    (hctx : ctx.WellFormed := by grind) : Nat :=
  match hparent : (op.get! ctx).parent with
  | some block =>
    (block.operationList ctx hctx (by grind)).size - 1 - op.idxInParent ctx hop hctx
  | none       => 0

theorem OperationPtr.idxInParentFromTail_next_eq
    (op : OperationPtr) (ctx : IRContext OpInfo) (nextOp : OperationPtr)
    (hnext : (op.get! ctx).next = some nextOp)
    (hnextOp : nextOp.InBounds ctx)
    (hop : op.InBounds ctx)
    (hctx : ctx.WellFormed) :
    nextOp.idxInParentFromTail ctx hnextOp hctx =
      op.idxInParentFromTail ctx hop hctx - 1 := by
  simp only [OperationPtr.idxInParentFromTail]
  simp only [OperationPtr.idxInParent_next_eq op ctx nextOp hnext hnextOp hop]
  split; grind
  split; rotate_left; grind
  rename_i nextParent block opParent
  have ⟨array, harray⟩ := hctx.opChain block (by grind)
  grind

grind_pattern OperationPtr.idxInParentFromTail_next_eq =>
  nextOp.idxInParentFromTail ctx hnextOp hctx, (op.get! ctx).next, some nextOp, ctx.WellFormed, op.InBounds ctx

theorem OperationPtr.idxInParentFromTail_next_ne_zero
    (op : OperationPtr) (ctx : IRContext OpInfo) (nextOp : OperationPtr)
    (hnext : (op.get! ctx).next = some nextOp)
    (hop : op.InBounds ctx)
    (hctx : ctx.WellFormed) :
    op.idxInParentFromTail ctx hop hctx ≠ 0 := by
  simp only [OperationPtr.idxInParentFromTail]
  split; rotate_left; grind
  rename_i block hblock
  have : (nextOp.get! ctx).parent = some block := by grind [IRContext.WellFormed]
  have := OperationPtr.idxInParent_lt_size_operationList nextOp ctx block (by grind) (by grind) hctx
  grind

grind_pattern OperationPtr.idxInParentFromTail_next_ne_zero =>
  op.idxInParentFromTail ctx hop hctx, (op.get! ctx).next, some nextOp, ctx.WellFormed, op.InBounds ctx

theorem OperationPtr.idxInParentFromTail_next_lt_idxInParentFromTail
    (op : OperationPtr) (ctx : IRContext OpInfo) (nextOp : OperationPtr)
    (hnext : (op.get! ctx).next = some nextOp)
    (hnextOp : nextOp.InBounds ctx)
    (hop : op.InBounds ctx)
    (hctx : ctx.WellFormed) :
    nextOp.idxInParentFromTail ctx hnextOp hctx <
      op.idxInParentFromTail ctx hop hctx := by
  simp only [OperationPtr.idxInParentFromTail_next_eq op ctx nextOp hnext hnextOp hop]
  grind

grind_pattern OperationPtr.idxInParentFromTail_next_eq =>
  nextOp.idxInParentFromTail ctx hnextOp hctx, (op.get! ctx).next, some nextOp, ctx.WellFormed, op.InBounds ctx

/--
  Prove preservation of the `region_parent` field of `OperationPtr.WellFormed`, if
  region parents, number of regions, and region pointers are unchanged in the
  new context.
-/
theorem OperationPtr.WellFormed.region_parent.unchanged
    {opPtr : OperationPtr} {ctx ctx' : IRContext OpInfo}
    (h_getRegion : opPtr.getRegion! ctx' = opPtr.getRegion! ctx)
    (h_numRegions : opPtr.getNumRegions! ctx' = opPtr.getNumRegions! ctx)
    (h_parent : (region.get! ctx').parent = (region.get! ctx).parent)
    (_h_inBounds : region.InBounds ctx)
    (h_wf : (∃ i, i < opPtr.getNumRegions! ctx ∧ opPtr.getRegion! ctx i = region) ↔
             (region.get! ctx).parent = some opPtr) :
    (∃ i, i < opPtr.getNumRegions! ctx' ∧ opPtr.getRegion! ctx' i = region) ↔
    (region.get! ctx').parent = some opPtr := by
  simp only [h_getRegion, h_numRegions, h_parent, h_wf]

/--
  An IR context that also carries its well-formedness proof.
  This is the type that users are expected to work with most of the time, unless they
  need to explicitly break the well-formedness invariant during a transformation.
-/
structure WfIRContext (OpInfo : Type) [HasOpInfo OpInfo] where
  raw : IRContext OpInfo
  wellFormed : raw.WellFormed

public instance {OpInfo} [HasOpInfo OpInfo] :
    Coe (WfIRContext OpInfo) (IRContext OpInfo) where
  coe wfCtx := wfCtx.raw

@[grind! .]
theorem WfIRContext_raw_wellFormed (wfCtx : WfIRContext OpInfo) :
    (wfCtx.raw).WellFormed := by
  grind [WfIRContext]

instance instWfIRContextInhabited {OpInfo} [HasOpInfo OpInfo] :
    Inhabited (WfIRContext OpInfo) where
  default := ⟨IRContext.empty OpInfo, IRContext.empty_wellFormed⟩

end Veir
