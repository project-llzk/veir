module

namespace Veir.Data.LLVM

public section

/--
The `Int` type can have any bitwidth `w`. It is either a two's complement
integer value of width `w` or a poison value indicating delayed undefined
bahavior.
-/
inductive Int (w : Nat) where
/-- A two's complement integer value of width `w`. -/
| val : BitVec w → Int w
/-- A poison value indicating delayed undefined behavior. -/
| poison : Int w
deriving DecidableEq, Inhabited

inductive IntPred where
  | eq
  | ne
  | ugt
  | uge
  | ult
  | ule
  | sgt
  | sge
  | slt
  | sle
deriving DecidableEq, Inhabited, Repr, Hashable

/-- Mapped as in MLIR:
  https://github.com/llvm/llvm-project/blob/d3417c8bf35852af88f41aa721a719ea756fdd8c/mlir/include/mlir/Dialect/LLVMIR/LLVMEnums.td#L571 -/
def IntPred.fromNat (s : Nat) : Option IntPred :=
  match s with
  | 0 => some .eq
  | 1 => some .ne
  | 2 => some .slt
  | 3 => some .sle
  | 4 => some .sgt
  | 5 => some .sge
  | 6 => some .ult
  | 7 => some .ule
  | 8 => some .ugt
  | 9 => some .uge
  | _ => none

/-- Mapped as in MLIR. See `IntPred.fromNat`. -/
def IntPred.toNat : IntPred → Nat
  | .eq => 0
  | .ne => 1
  | .slt => 2
  | .sle => 3
  | .sgt => 4
  | .sge => 5
  | .ult => 6
  | .ule => 7
  | .ugt => 8
  | .uge => 9

/-- Sanity check: A numeric code parses to a predicate exactly when it
    is that predicate's MLIR code. -/
theorem IntPred.fromNat_eq_some_iff {n : Nat} {p : IntPred} :
    IntPred.fromNat n = some p ↔ p.toNat = n := by
  cases p <;> simp only [IntPred.fromNat, IntPred.toNat] <;> grind

def IntPred.eval (p : IntPred) (x y : BitVec w) : Bool :=
  match p with
  | .eq => x == y
  | .ne => x != y
  | .ugt => y.ult x
  | .uge => y.ule x
  | .ult => x.ult y
  | .ule => x.ule y
  | .sgt => y.slt x
  | .sge => y.sle x
  | .slt => x.slt y
  | .sle => x.sle y

namespace Int

instance {w : Nat} : ToString (Int w) where
  toString
    | .val v => toString v
    | .poison => "poison"

/--
  We define the semantics of a `constant` operation.
  The result of this operation is never poison.
-/
def constant (w : Nat) (v : _root_.Int) : Int w := val (BitVec.ofInt w v)

/--
  We define the semantics of a `poison` operation.
  The result of this operation is always poison.
-/
def mlir_poison (w : Nat) : Int w := poison

/-- The unsigned value of the integer, or `none` if it is poison. -/
def toNat? {w : Nat} : Int w → Option Nat
  | .val v => some v.toNat
  | .poison => none

/--
The ‘add’ instruction returns the sum of its two operands.

If the sum has unsigned overflow, the result returned is the mathematical result
modulo 2^n, where n is the bit width of the result.

Because LLVM integers use a two’s complement representation, this instruction is
appropriate for both signed and unsigned integers.

`nuw` and `nsw` stand for “No Unsigned Wrap” and “No Signed Wrap”, respectively.
If the `nuw` and/or `nsw` arguments are true, the result value of the add is a
poison value if unsigned and/or signed overflow, respectively, occurs.
-/
def add {w : Nat} (x y : Int w) (nsw : Bool := false) (nuw : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if nsw ∧ BitVec.saddOverflow x' y' then
    return poison

  if nuw ∧ BitVec.uaddOverflow x' y' then
    return poison

  val (x' + y')

/--
The overflow (carry-out) bit of an unsigned addition, as produced by the second
result of `arith.addui_extended`. This lowers to the `i1` overflow value of
`llvm.uadd.with.overflow`: the result is `1` when the unsigned sum of `x` and `y`
does not fit in `w` bits, and `0` otherwise. If either operand is poison, the
result is poison.
-/
def uaddOverflowFlag {w : Nat} (x y : Int w) : Int 1 := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (BitVec.ofBool (BitVec.uaddOverflow x' y'))

/--
The `sub` instruction returns the difference of its two operands.

Note that the `sub` instruction is used to represent the `neg` instruction
present in most other intermediate representations.

The value produced is the integer difference of the two operands.

If the difference has unsigned overflow, the result returned is the mathematical
result modulo `2^w`, where `w` is the bit width of the result.

Because LLVM integers use a two’s complement representation, this instruction is
appropriate for both signed and unsigned integers.

`nuw` and `nsw` stand for “No Unsigned Wrap” and “No Signed Wrap”, respectively.
If the `nuw` and/or `nsw` arguments are true, the result value of the sub is a
poison value if unsigned and/or signed overflow, respectively, occurs.
-/
def sub {w : Nat} (x y : Int w) (nsw : Bool := false) (nuw : Bool := false) :
    Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if nsw ∧ BitVec.ssubOverflow x' y' then
    return poison

  if nuw ∧ BitVec.usubOverflow x' y' then
    return poison

  val (x' - y')

/--
The borrow bit of an unsigned subtraction, as produced by the second result of
`arith.subui_extended`. This lowers to the `i1` overflow value of
`llvm.usub.with.overflow`: the result is `1` when the unsigned difference of `x`
and `y` underflows, i.e. when `x < y` unsigned, and `0` otherwise. If either
operand is poison, the result is poison.
-/
def usubOverflowFlag {w : Nat} (x y : Int w) : Int 1 := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (BitVec.ofBool (BitVec.usubOverflow x' y'))

/--
`llvm.sadd.sat`: signed saturating addition. The result is the mathematical sum
of the operands clamped to the signed range of the bit width. Signed addition
can only overflow when both operands share a sign, so on overflow the result
saturates to the smallest signed value (`intMin`) when the operands are negative
and to the largest signed value (`intMax`) when they are non-negative. If either
operand is poison, the result is poison.
-/
def saddSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if BitVec.saddOverflow x' y' then
    return val (if x'.msb then BitVec.intMin w else BitVec.intMax w)
  val (x' + y')

/--
`llvm.uadd.sat`: unsigned saturating addition. On unsigned overflow the result
saturates to the largest unsigned value (all ones). Because this is an unsigned
operation the result never saturates towards zero. If either operand is poison,
the result is poison.
-/
def uaddSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if BitVec.uaddOverflow x' y' then
    return val (BitVec.allOnes w)
  val (x' + y')

/--
`llvm.ssub.sat`: signed saturating subtraction. On signed overflow the result
saturates to the smallest signed value (`intMin`) when the first operand is
negative and to the largest signed value (`intMax`) when it is non-negative:
subtraction only overflows when the operands differ in sign, and the overflow
direction is then determined by the sign of the minuend. If either operand is
poison, the result is poison.
-/
def ssubSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if BitVec.ssubOverflow x' y' then
    return val (if x'.msb then BitVec.intMin w else BitVec.intMax w)
  val (x' - y')

/--
`llvm.usub.sat`: unsigned saturating subtraction. On unsigned borrow (the
subtrahend exceeds the minuend) the result saturates to zero. Because this is an
unsigned operation the result never saturates towards the maximum. If either
operand is poison, the result is poison.
-/
def usubSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if BitVec.usubOverflow x' y' then
    return val (BitVec.ofNat w 0)
  val (x' - y')

/--
`llvm.sshl.sat`: signed saturating left shift of `x` by `y`. If `y` is equal to
or larger than the bit width the result is poison. Otherwise the result is
`x <<< y`, saturated to the largest signed value (`intMax`) when `x` is
non-negative and the smallest signed value (`intMin`) when `x` is negative,
whenever the shift changes the value's arithmetic meaning (i.e. bits other than
copies of the sign bit are shifted out). If `x` is poison, the result is poison.
-/
def sshlSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if y' ≥ w then
    return poison
  let shifted := x' <<< y'
  if shifted.sshiftRight' y' ≠ x' then
    return val (if x'.msb then BitVec.intMin w else BitVec.intMax w)
  val shifted

/--
`llvm.ushl.sat`: unsigned saturating left shift of `x` by `y`. If `y` is equal to
or larger than the bit width the result is poison. Otherwise the result is
`x <<< y`, saturated to the largest unsigned value (all ones) whenever any set
bit is shifted out (i.e. the shift is not losslessly reversible). If `x` is
poison, the result is poison.
-/
def ushlSat {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  if y' ≥ w then
    return poison
  let shifted := x' <<< y'
  if shifted >>> y' ≠ x' then
    return val (BitVec.allOnes w)
  val shifted

/--
The ‘mul’ instruction returns the product of its two operands.

If the result of the multiplication has unsigned overflow, the result returned
is the mathematical result modulo 2^n, where n is the bit width of the result.

Because LLVM integers use a two’s complement representation, and the result is
the same width as the operands, this instruction returns the correct result for
both signed and unsigned integers. If a full product (e.g., i32 * i32 -> i64) is
needed, the operands should be sign-extended or zero-extended as appropriate to
the width of the full product.

`nuw` and `nsw` stand for “No Unsigned Wrap” and “No Signed Wrap”, respectively. If
the `nuw` and/or `nsw` arguments are true, the result value of the mul is a poison
value if unsigned and/or signed overflow, respectively, occurs.
-/
def mul {w : Nat} (x y : Int w) (nsw : Bool := false) (nuw : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if nsw ∧ BitVec.smulOverflow x' y' then
    return poison

  if nuw ∧ BitVec.umulOverflow x' y' then
    return poison

  val (x' * y')

/--
The high half of the `2 * w`-bit unsigned product of `x` and `y`, as produced by
the second (`high`) result of `arith.mului_extended`. The operands are
zero-extended to `2 * w` bits, multiplied, and the high `w` bits (positions
`w …< 2*w`) are returned. The corresponding low half is `mul x y`. If either
operand is poison, the result is poison.
-/
def umulHigh {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  let wide : BitVec (w + w) := x'.zeroExtend (w + w) * y'.zeroExtend (w + w)
  val (wide.extractLsb' w w)

/--
The high half of the `2 * w`-bit signed product of `x` and `y`, as produced by
the second (`high`) result of `arith.mulsi_extended`. The operands are
sign-extended to `2 * w` bits, multiplied, and the high `w` bits (positions
`w …< 2*w`) are returned. The corresponding low half is `mul x y`. If either
operand is poison, the result is poison.
-/
def smulHigh {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  let wide : BitVec (w + w) := x'.signExtend (w + w) * y'.signExtend (w + w)
  val (wide.extractLsb' w w)

/--
The ‘udiv’ instruction returns the unsigned integer quotient of its two operands.

Note that unsigned integer division and signed integer division are distinct
operations; for signed integer division, use ‘sdiv’.

Division by zero is undefined behavior. For vectors, if any element of the
divisor is zero, the operation has undefined behavior.

If the `exact` argument is true, the result value of the udiv is a poison value
if `x` is not a multiple of `y` (as such, “((a udiv exact b) mul b) == a”).
-/
def udiv {w : Nat} (x y : Int w) (exact : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if exact ∧ x'.umod y' ≠ 0 then
    return poison

  if y' = 0 then
    return poison

  val (x' / y')

/--
The ‘sdiv’ instruction returns the quotient of its two operands.

The value produced is the signed integer quotient of the two operands rounded
towards zero.

Note that signed integer division and unsigned integer division are distinct
operations; for unsigned integer division, use ‘udiv’.

Division by zero is undefined behavior. For vectors, if any element of the
divisor is zero, the operation has undefined behavior. Overflow also leads to
undefined behavior; this is a rare case, but can occur, for example, by doing a
32-bit division of -2147483648 by -1.

If the `exact` argument is true, the result value of the sdiv is a poison value
if the result would be rounded.
-/
def sdiv {w : Nat} (x y : Int w) (exact : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' == 0 || (x' == (BitVec.intMin w) && y' == -1) then
    return poison

  if exact ∧ x'.smod y' ≠ 0 then
    return poison

  val (x'.sdiv y')

/--
The ‘urem’ instruction returns the unsigned integer remainder from the
unsigned division of its two arguments. This instruction always performs
an unsigned division to get the remainder.

Note that unsigned integer remainder and signed integer remainder are distinct
operations; for signed integer remainder, use ‘srem’.

Taking the remainder of a division by zero is undefined behavior. For vectors,
if any element of the divisor is zero, the operation has undefined behavior.
-/
def urem {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' == 0 then
    return poison

  val (x' % y')

/--
The ‘srem’ instruction returns the remainder from the signed division of its two
operands.

This instruction returns the remainder of a division (where the result is either
zero or has the same sign as the dividend, `x`), not the modulo operator (where
the result is either zero or has the same sign as the divisor, `y`) of a value.

Note that signed integer remainder and unsigned integer remainder are distinct
operations; for unsigned integer remainder, use ‘urem’.

Taking the remainder of a division by zero is undefined behavior. For vectors,
if any element of the divisor is zero, the operation has undefined behavior.
Overflow also leads to undefined behavior; this is a rare case, but can occur,
for example, by taking the remainder of a 32-bit division of -2147483648 by -1.
(The remainder doesn’t actually overflow, but this rule lets srem be implemented
using instructions that return both the result of the division and the
remainder.)
-/
def srem {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' == 0 || (x' == (BitVec.intMin w) && y' == -1) then
    return poison

  val (x'.srem y')

/--
The ‘shl’ instruction returns the first operand shifted to the left by a specified
number of bits.

The value produced is `x` * 2^`y` mod 2^n, where n is the width of the result.
If `y` is (statically or dynamically) equal to or larger than the number of bits
in `x`, this instruction returns a poison value. If the arguments are vectors,
each vector element of `x` is shifted by the corresponding shift amount in `y`.

If the `nuw` keyword is present, then the shift produces a poison value if it
shifts out any non-zero bits. If the `nsw` keyword is present, then the shift
produces a poison value if it shifts out any bits that disagree with the
resultant sign bit.
-/
def shl {w : Nat} (x y : Int w) (nsw : Bool := false) (nuw : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' ≥ w then
    return poison

  if nsw ∧ (x' <<< y').sshiftRight' y' ≠ x' then
    return poison

  if nuw ∧ (x' <<< y') >>> y' ≠ x' then
    return poison

  val (x' <<< y')

/--
The ‘lshr’ instruction (logical shift right) returns the first operand shifted
to the right a specified number of bits with zero fill.

This instruction always performs a logical shift right operation. The most
significant bits of the result will be filled with zero bits after the shift. If
`y` is (statically or dynamically) equal to or larger than the number of bits in
`x`, this instruction returns a poison value. If the arguments are vectors, each
vector element of `x` is shifted by the corresponding shift amount in `y`.

If the `exact` argument is true, the result value of the lshr is a poison value
if any of the bits shifted out are non-zero.
-/
def lshr {w : Nat} (x y : Int w) (exact : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' ≥ w then
    return poison

  if exact ∧ (x' >>> y') <<< y' ≠ x' then
    return poison

  val (x' >>> y')

/--
The ‘ashr’ instruction (arithmetic shift right) returns the first operand
shifted to the right a specified number of bits with sign extension.

This instruction always performs an arithmetic shift right operation, The most
significant bits of the result will be filled with the sign bit of `x`. If `y`
is (statically or dynamically) equal to or larger than the number of bits in
`x`, this instruction returns a poison value. If the arguments are vectors, each
vector element of `x` is shifted by the corresponding shift amount in `y`.

If the `exact` argument is true, the result value of the ashr is a poison value
if any of the bits shifted out are non-zero.
-/
def ashr {w : Nat} (x y : Int w) (exact : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if y' ≥ w then
    return poison

  if exact ∧ (x' >>> y') <<< y' ≠ x' then
    return poison

  val (x'.sshiftRight' y')

/--
The ‘fshl’ (funnel shift left) operation concatenates `a` (high part) and `b`
(low part) into a value twice the bit width, shifts it left by `c` (taken modulo
the bit width), and returns the most significant half of the result.

When `a = b` this is a left rotate. The shift amount is always interpreted modulo
the bit width, so it never causes poison on its own; poison only results from a
poison operand.
-/
def fshl {w : Nat} (a b c : Int w) : Int w := Id.run do
  let val a' := a | poison
  let val b' := b | poison
  let val c' := c | poison

  let s := c'.toNat % w
  -- Concatenate into a `2 * w`-bit value with `a` as the high half and `b` as
  -- the low half, shift left, and keep the high `w` bits (positions `w …< 2*w`).
  let wide : BitVec (w + w) := a' ++ b'
  val ((wide <<< s).extractLsb' w w)

/--
The ‘fshr’ (funnel shift right) operation concatenates `a` (high part) and `b`
(low part) into a value twice the bit width, shifts it right by `c` (taken modulo
the bit width), and returns the least significant half of the result.

When `a = b` this is a right rotate. The shift amount is always interpreted modulo
the bit width, so it never causes poison on its own; poison only results from a
poison operand.
-/
def fshr {w : Nat} (a b c : Int w) : Int w := Id.run do
  let val a' := a | poison
  let val b' := b | poison
  let val c' := c | poison

  let s := c'.toNat % w
  -- Concatenate into a `2 * w`-bit value with `a` as the high half and `b` as
  -- the low half, shift right, and keep the low `w` bits (positions `0 …< w`).
  let wide : BitVec (w + w) := a' ++ b'
  val ((wide >>> s).truncate w)

/--
The `ctlz` intrinsic counts leading zero bits. If `is_zero_poison` is true,
then a zero input produces poison.
-/
def ctlz {w : Nat} (x : Int w) (is_zero_poison : Bool) : Int w := Id.run do
  let val x' := x | poison
  if is_zero_poison ∧ x' = 0 then
    return poison
  val (BitVec.clz x')

/--
The `cttz` intrinsic counts trailing zero bits. If `is_zero_poison` is true,
then a zero input produces poison.
-/
def cttz {w : Nat} (x : Int w) (is_zero_poison : Bool) : Int w := Id.run do
  let val x' := x | poison
  if is_zero_poison ∧ x' = 0 then
    return poison
  val (BitVec.ctz x')

/-- The `ctpop` intrinsic counts set bits. -/
def ctpop {w : Nat} (x : Int w) : Int w := Id.run do
  let val x' := x | poison
  val (BitVec.cpop x')

def bswap16BV (x : BitVec 16) : BitVec 16 :=
  x.extractLsb 7 0 ++ x.extractLsb 15 8

def bswap32BV (x : BitVec 32) : BitVec 32 :=
  x.extractLsb 7 0 ++ x.extractLsb 15 8 ++
  x.extractLsb 23 16 ++ x.extractLsb 31 24

def bswap64BV (x : BitVec 64) : BitVec 64 :=
  x.extractLsb 7 0 ++ x.extractLsb 15 8 ++
  x.extractLsb 23 16 ++ x.extractLsb 31 24 ++
  x.extractLsb 39 32 ++ x.extractLsb 47 40 ++
  x.extractLsb 55 48 ++ x.extractLsb 63 56

/--
The `bswap` intrinsic reverses byte order. Only 16, 32, and 64-bit operands
are supported; the verifier rejects every other width, so the final branch is
unreachable for valid IR.
-/
def bswap {w : Nat} (x : Int w) : Int w := Id.run do
  let val x' := x | poison
  if h : w = 16 then
    return val ((bswap16BV (x'.cast h)).cast h.symm)
  if h : w = 32 then
    return val ((bswap32BV (x'.cast h)).cast h.symm)
  if h : w = 64 then
    return val ((bswap64BV (x'.cast h)).cast h.symm)
  val x'

/-- The `bitreverse` intrinsic reverses bit order. -/
def bitreverse {w : Nat} (x : Int w) : Int w := Id.run do
  let val x' := x | poison
  val (BitVec.reverse x')

def cast {w₁ w₂ : Nat} (x : Int w₁) (h : w₁ = w₂) : Int w₂ :=
  match x with
  | .val v => .val (v.cast h)
  | .poison => .poison

@[simp, grind =]
theorem cast_self {w : Nat} (x : Int w) (h : w = w) : cast x h = x := by
  cases x <;> simp [cast]

/--
The ‘and’ instruction returns the bitwise logical and of its two operands.

The truth table used for the ‘and’ instruction is:

   In0 In1 Out
    0   0   0
    0   1   0
    1   0   0
    1   1   1
-/
def and {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  val (x' &&& y')


/--
The ‘or’ instruction returns the bitwise logical inclusive or of its two operands.

The truth table used for the ‘or’ instruction is:

   In0 In1 Out
    0   0   0
    0   1   1
    1   0   1
    1   1   1

`disjoint` means that for each bit, that bit is zero in at least one of the
inputs. This allows the Or to be treated as an Add since no carry can occur from
any bit. If the `disjoint` keyword is present, the result value of the or is a
poison value if both inputs have a one in the same bit position. For vectors,
any bit. If the `disjoint` argument is true, the result value of the or is a
poison value if both inputs have a one in the same bit position. For vectors,
-/
def or {w : Nat} (x y : Int w) (disjoint : Bool := false) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison

  if disjoint ∧ (x' &&& y') ≠ 0 then
    return poison

  val (x' ||| y')

/--
The `xor` instruction returns the bitwise logical exclusive or of its two
operands. The xor is used to implement the "one's complement" operation, which
is the "~" operator in C.

The truth table used for the ‘xor’ instruction is:

    In0 In1 Out
      0   0   0
      0   1   1
      1   0   1
      1   1   0
-/
def xor {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (x' ^^^ y')

/--
The `smax` intrinsic returns the larger of its two operands, treating them as
signed integers. If either operand is poison, the result is poison.
-/
def smax {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (if x'.sle y' then y' else x')

/--
The `smin` intrinsic returns the smaller of its two operands, treating them as
signed integers. If either operand is poison, the result is poison.
-/
def smin {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (if x'.sle y' then x' else y')

/--
The `umax` intrinsic returns the larger of its two operands, treating them as
unsigned integers. If either operand is poison, the result is poison.
-/
def umax {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (if x'.ule y' then y' else x')

/--
The `umin` intrinsic returns the smaller of its two operands, treating them as
unsigned integers. If either operand is poison, the result is poison.
-/
def umin {w : Nat} (x y : Int w) : Int w := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (if x'.ule y' then x' else y')

/--
The `abs` intrinsic returns the magnitude (always non-negative) of its argument.
Because `-intMin` is not representable, when the argument is `intMin` the result
is `intMin` if `is_int_min_poison` is false and poison if it is true. If the
argument is poison, the result is poison.
-/
def abs {w : Nat} (x : Int w) (is_int_min_poison : Bool) : Int w := Id.run do
  let val x' := x | poison
  if is_int_min_poison ∧ x' = BitVec.intMin w then
    return poison
  val (if x'.msb then -x' else x')

/--
The `trunc` instruction truncates the high order bits in value and converts the
remaining bits to `w₂`. Since the source size must be larger than the
destination size, trunc cannot be a no-op cast. It will always truncate bits.

If the `nuw` keyword is present, and any of the truncated bits are non-zero, the
result is a poison value. If the `nsw` keyword is present, and any of the
truncated bits are not the same as the top bit of the truncation result, the
result is a poison value.
-/
def trunc {w₁ : Nat} (x : Int w₁) (w₂ : Nat) (nsw : Bool := false) (nuw : Bool := false) (_h : w₁ > w₂) : Int w₂ := Id.run do
  let val v := x | poison

  if nsw && (v.truncate w₂).signExtend w₁ ≠ v then
    return poison

  if nuw && (v.truncate w₂).zeroExtend w₁ ≠ v then
    return poison

  val (v.truncate w₂)

/--
The 'zext' instruction zero-extends its operand to the given type.

The `zext` fills the high order bits of the value with zero bits until it reaches
the size of the destination type, ty2.

When `zero` extending from i1, the result will always be either 0 or 1.

If the `nneg` flag is set, and the zext argument is negative, the result is a
poison value.
-/
def zext {w₁ : Nat} (x : Int w₁) (w₂ : Nat) (nneg : Bool := false) (_h : w₁ < w₂) : Int w₂ := Id.run do
  let val v := x | poison

  if nneg && v.msb then
    return poison

  val (v.zeroExtend w₂)

/--
The `sext` instruction sign-extends its operand to the given type.

The `sext` instruction performs a sign extension by copying the sign bit
(highest order bit) of the value until it reaches the bit size of the type `w₂`.

When sign extending from i1, the extension always results in -1 or 0.
-/
def sext {w₁ : Nat} (x : Int w₁) (w₂ : Nat) (_h : w₁ < w₂) : Int w₂ := Id.run do
  let val v := x | poison

  val (v.signExtend w₂)

/--
`ext` extends the given `Int` to the specified bitwidth, using the given MSBs.

Combining it with a ∀ quantifier allows one to model the `G_ANYEXT` gMIR instruction
that extends its operand with unspecified bits.
-/
def ext {w₁ : Nat} (x : Int w₁) (w₂ : Nat) (msb : BitVec (w₂-w₁)) (_h : w₁ < w₂) : Int w₂ := Id.run do
  let val v := x | poison
  (val (msb ++ v)).cast (by grind)

/--
The `icmp` instruction takes three operands.
The first operand is the condition code indicating the kind of comparison to perform.
It is not a value, just a keyword.
The possible condition codes (of type `IntPred`)are:

  - `eq`: equal
  - `ne`: not equal
  - `ugt`: unsigned greater than
  - `uge`: unsigned greater or equal
  - `ult`: unsigned less than
  - `ule`: unsigned less or equal
  - `sgt`: signed greater than
  - `sge`: signed greater or equal
  - `slt`: signed less than
  - `sle`: signed less or equal

The remaining two arguments must be integer. They must also be identical types.
-/
def icmp {w : Nat} (x y : Int w) (p : IntPred) : Int 1 := Id.run do
  let val x' := x | poison
  let val y' := y | poison
  val (BitVec.ofBool (IntPred.eval p x' y'))

/--
 If the condition is an i1 and it evaluates to 1, the instruction returns the first value argument; otherwise, it returns the second value argument.

 If the condition is poison, the result is poison. Poison on the *non-selected* arm
 does not propagate to the result.
-/
def select {w : Nat} (c : Int 1) (x y : Int w) : Int w := Id.run do
  let val c' := c | poison
  if c' == 1#1 then x else y


/--
 The `freeze` instruction converts a poison value to a non-poison value by
 replacing it with an arbitrary value. We currently always pick zero.
-/
def freeze {w : Nat} (x : Int w) : Int w := Id.run do
  match x with
  | .val v => .val v
  | .poison => .val 0

end Int
end
end Veir.Data.LLVM
