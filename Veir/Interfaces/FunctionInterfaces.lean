module

public import Veir.Rewriter.WfRewriter

/-!
# FunctionOpInterface

This file provides the `FunctionOpInterface` interface, which provides support
for interacting with operations that behave like functions.
Currently, this supports llvm.func and func.func.

Also see:
https://github.com/llvm/llvm-project/blob/main/mlir/include/mlir/Interfaces/FunctionInterfaces.td
-/

namespace Veir

variable {OpCode : Type} [HasOpInfo OpCode]

public section

/-- Whether this operation acts like a function. -/
def OperationPtr.isFunctionLike (op : OperationPtr) (ctx : IRContext OpCode) : Bool :=
  (HasOpInfo.functionInterface? (op.getOpType! ctx)).isSome

namespace FunctionOpInterface

/-- Returns the symbol name of the function. -/
def getSymName? (funcOp : OperationPtr) (raw : IRContext OpCode) : Option StringAttr := do
  let opType := funcOp.getOpType! raw
  let interface ← HasOpInfo.functionInterface? opType
  return interface.getSymName (funcOp.getProperties! raw opType)

/-- Returns the type of the function. -/
def getFunctionType? (funcOp : OperationPtr) (raw : IRContext OpCode) : Option FunctionType := do
  let opType := funcOp.getOpType! raw
  let interface ← HasOpInfo.functionInterface? opType
  return interface.getFunctionType (funcOp.getProperties! raw opType)

/-!
## Body Handling
-/

/-- Returns the region containing the body of this function. -/
def getFunctionBody (funcOp : OperationPtr) (raw : IRContext OpCode)
    (opInBounds : funcOp.InBounds raw := by grind)
    (hasRegion : 0 < funcOp.getNumRegions raw opInBounds := by grind) : RegionPtr :=
  funcOp.getRegion raw 0 opInBounds hasRegion

/-- Returns the region containing the body of this function. -/
def getFunctionBody! (funcOp : OperationPtr) (raw : IRContext OpCode) : RegionPtr :=
  funcOp.getRegion! raw 0

@[grind =_, eq_bang ←]
theorem getFunctionBody!_eq_getFunctionBody {funcOp : OperationPtr} {raw : IRContext OpCode}
    {opInBounds} (hasRegion : 0 < funcOp.getNumRegions raw opInBounds) :
    getFunctionBody! funcOp raw = getFunctionBody funcOp raw opInBounds hasRegion := by
  grind [getFunctionBody, getFunctionBody!]

theorem getFunctionBody!_inBounds
    {raw : IRContext OpCode}
    (ctxInBounds : raw.FieldsInBounds)
    (opInBounds : funcOp.InBounds raw)
    (hasRegion : 0 < funcOp.getNumRegions! raw) :
    (getFunctionBody! funcOp raw).InBounds raw := by
  grind [getFunctionBody!, OperationPtr.getRegions!_inBounds]

grind_pattern getFunctionBody!_inBounds => (getFunctionBody! funcOp raw), raw.FieldsInBounds

/-- Returns the first block in the body region. -/
def getEntryBlock? (funcOp : OperationPtr) (raw : IRContext OpCode) : Option BlockPtr :=
  ((getFunctionBody! funcOp raw).get! raw).firstBlock

/-!
## Type Attribute Handling
-/

/-- Sets the function type to the given input/output type lists. -/
def setFunctionTypeInBounds (wfCtx : WfIRContext OpCode) (funcOp : OperationPtr)
    (inputs outputs : Array Attribute)
    (opInBounds : funcOp.InBounds wfCtx.raw := by grind) : WfIRContext OpCode :=
  let opType := funcOp.getOpType! wfCtx.raw
  match HasOpInfo.functionInterface? opType with
  | none => wfCtx
  | some interface =>
    let props := funcOp.getProperties! wfCtx.raw opType
    let newProps := interface.setFunctionType props { inputs, outputs }
    WfRewriter.setProperties wfCtx funcOp opType newProps opInBounds

/-- Sets the function type to the given input/output type lists, panicking if the op
    is out of bounds. -/
def setFunctionType! (c : WfIRContext OpCode) (funcOp : OperationPtr)
    (inputs outputs : Array Attribute) : WfIRContext OpCode :=
  if opInBounds : funcOp.InBounds c.raw then
    setFunctionTypeInBounds c funcOp inputs outputs opInBounds
  else
    panic "FunctionOpInterface.setFunctionType! failed: operation is out of bounds"

/-!
## Argument and Result Handling
-/

/-- Returns the number of function arguments. -/
def getNumArguments? (funcOp : OperationPtr) (raw : IRContext OpCode) : Option Nat := do
  rlet ft ← getFunctionType? funcOp raw
  ft.inputs.size

/-- Returns the argument types of the function. -/
def getArgumentTypes? (funcOp: OperationPtr) (raw: IRContext OpCode) :
    Option (Array Attribute) := do
  rlet ft ← getFunctionType? funcOp raw
  ft.inputs

/-- Returns the number of function results. -/
def getNumResults? (funcOp : OperationPtr) (raw : IRContext OpCode) : Option Nat := do
  rlet ft ← getFunctionType? funcOp raw
  ft.outputs.size

/-- Returns the result types of the function. -/
def getResultTypes? (funcOp : OperationPtr) (raw : IRContext OpCode) :
    Option (Array Attribute) := do
  rlet ft ← getFunctionType? funcOp raw
  ft.outputs

/-- Returns the result types of the function. -/
def getResultTypes! (funcOp : OperationPtr) (raw : IRContext OpCode) : Array Attribute :=
  (getResultTypes? funcOp raw).get!

end FunctionOpInterface

end

end Veir
