module

public import Veir.IR.Simp
public import Veir.IR.OpInfo
public import Veir.Verifier.Basic
public import Veir.Dialects.RISCV.Properties
public import Veir.ConstantMaterialization
meta import Veir.Meta.OpCode

namespace Veir

public section

@[opcodes]
inductive Riscv where
| li
| lui
| auipc
| addi
| slti
| sltiu
| andi
| ori
| xori
| addiw
| slli
| srli
| srai
| add
| sub
| sll
| slt
| sltu
| xor
| srl
| sra
| or
| and
| slliw
| srliw
| sraiw
| addw
| subw
| sllw
| srlw
| sraw
| rem
| remu
| remw
| remuw
| mul
| mulh
| mulhu
| mulhsu
| mulw
| div
| divw
| divu
| divuw
| adduw
| sh1adduw
| sh2adduw
| sh3adduw
| sh1add
| sh2add
| sh3add
| slliuw
| andn
| orn
| xnor
| max
| maxu
| min
| minu
| rol
| ror
| rolw
| rorw
| sextb
| sexth
| zexth
| clz
| clzw
| ctz
| ctzw
| cpop
| cpopw
| orcb
| rev8
| roriw
| rori
| bclr
| bext
| binv
| bset
| bclri
| bexti
| binvi
| bseti
| pack
| packh
| packw
| czeroeqz
| czeronez
/- memory -/
| ld
| lw
| lwu
| lh
| lhu
| lb
| lbu
| sd
| sw
| sh
| sb

/- pseudooperations -/
| mv
| not
| neg
| negw
| sextw
| zextb
| zextw
| seqz
| snez
| sltz
| sgtz
deriving Inhabited, Repr, Hashable, DecidableEq

@[expose, properties_of]
def Riscv.propertiesOf (op : Riscv) : Type :=
match op with
| .li => RISCVImmediateProperties
| .lui => RISCVImmediateProperties
| .auipc => RISCVImmediateProperties
| .andi => RISCVImmediateProperties
| .ori => RISCVImmediateProperties
| .xori => RISCVImmediateProperties
| .addi => RISCVImmediateProperties
| .slti => RISCVImmediateProperties
| .sltiu => RISCVImmediateProperties
| .addiw => RISCVImmediateProperties
| .slli => RISCVImmediateProperties
| .srli => RISCVImmediateProperties
| .srai => RISCVImmediateProperties
| .slliw => RISCVImmediateProperties
| .srliw => RISCVImmediateProperties
| .sraiw => RISCVImmediateProperties
| .slliuw => RISCVImmediateProperties
| .rori => RISCVImmediateProperties
| .roriw => RISCVImmediateProperties
| .bclri => RISCVImmediateProperties
| .bexti => RISCVImmediateProperties
| .binvi => RISCVImmediateProperties
| .bseti => RISCVImmediateProperties
/- The memory ops carry an offset immediate plus a volatile flag. -/
| .ld => RISCVMemProperties
| .lw => RISCVMemProperties
| .lwu => RISCVMemProperties
| .lh => RISCVMemProperties
| .lhu => RISCVMemProperties
| .lb => RISCVMemProperties
| .lbu => RISCVMemProperties
| .sd => RISCVMemProperties
| .sw => RISCVMemProperties
| .sh => RISCVMemProperties
| .sb => RISCVMemProperties
| _ => Unit

def Riscv.fromAttrDict
    (op : Riscv) (attrDict : Std.HashMap ByteArray Attribute) :
    Except String (Riscv.propertiesOf op) := by
  cases op
  case li | lui | auipc | andi | ori | xori | addi | slti | sltiu
      | addiw | slli | srli | srai | slliw | srliw | sraiw | slliuw
      | rori | roriw | bclri | bexti | binvi | bseti =>
    exact RISCVImmediateProperties.fromAttrDict attrDict
  case ld | lw | lwu | lh | lhu | lb | lbu | sd | sw | sh | sb =>
    exact RISCVMemProperties.fromAttrDict attrDict
  all_goals exact .ok ()

def Riscv.toAttrDict
    (op : Riscv) (props : Riscv.propertiesOf op) :
    Std.HashMap ByteArray Attribute :=
  match op with
  | .li | .lui | .auipc | .andi | .ori | .xori
  | .addi | .slti | .sltiu | .addiw | .slli | .srli | .srai
  | .slliw | .srliw | .sraiw | .rori | .roriw | .slliuw
  | .bclri | .bexti | .binvi | .bseti =>
    (Std.HashMap.emptyWithCapacity 2).insert
      "value".toUTF8 (Attribute.integerAttr props.value)
  -- The memory ops additionally carry a volatile flag, printed only when set.
  | .ld | .lw | .lwu | .lh | .lhu | .lb | .lbu
  | .sd | .sw | .sh | .sb => Id.run do
    let mut dict := Std.HashMap.emptyWithCapacity 2
    dict := dict.insert "value".toUTF8 (Attribute.integerAttr props.value)
    if props.volatile_ then
      dict := dict.insert "volatile_".toUTF8 (.unitAttr UnitAttr.mk)
    dict
  | _ => Std.HashMap.emptyWithCapacity 0

def Riscv.getEffects (op : Riscv) (props : Riscv.propertiesOf op) : MemoryEffects :=
  match op, props with
  | .ld, props | .lw, props | .lwu, props
  | .lh, props | .lhu, props
  | .lb, props | .lbu, props =>
    if props.volatile_ then .readWrite else .read
  | .sd, props | .sw, props
  | .sh, props | .sb, props =>
    if props.volatile_ then .readWrite else .write
  | .li, _ | .lui, _ | .auipc, _
  | .addi, _ | .slti, _ | .sltiu, _
  | .andi, _ | .ori, _ | .xori, _
  | .addiw, _ | .slli, _ | .srli, _ | .srai, _
  | .add, _ | .sub, _ | .sll, _ | .slt, _ | .sltu, _
  | .xor, _ | .srl, _ | .sra, _ | .or, _ | .and, _
  | .slliw, _ | .srliw, _ | .sraiw, _
  | .addw, _ | .subw, _ | .sllw, _ | .srlw, _ | .sraw, _
  | .rem, _ | .remu, _ | .remw, _ | .remuw, _
  | .mul, _ | .mulh, _ | .mulhu, _ | .mulhsu, _ | .mulw, _
  | .div, _ | .divw, _ | .divu, _ | .divuw, _
  | .adduw, _ | .sh1adduw, _ | .sh2adduw, _ | .sh3adduw, _
  | .sh1add, _ | .sh2add, _ | .sh3add, _ | .slliuw, _
  | .andn, _ | .orn, _ | .xnor, _
  | .max, _ | .maxu, _ | .min, _ | .minu, _
  | .rol, _ | .ror, _ | .rolw, _ | .rorw, _
  | .sextb, _ | .sexth, _ | .zexth, _
  | .clz, _ | .clzw, _ | .ctz, _ | .ctzw, _
  | .cpop, _ | .cpopw, _ | .orcb, _ | .rev8, _
  | .rori, _ | .roriw, _
  | .bclr, _ | .bext, _ | .binv, _ | .bset, _
  | .bclri, _ | .bexti, _ | .binvi, _ | .bseti, _
  | .pack, _ | .packh, _ | .packw, _
  | .czeroeqz, _ | .czeronez, _
  | .mv, _ | .not, _ | .neg, _ | .negw, _
  | .sextw, _ | .zextb, _ | .zextw, _
  | .seqz, _ | .snez, _ | .sltz, _ | .sgtz, _ => .none

def Riscv.isConstantLike (op : Riscv) : Bool :=
  match op with
  | .li | .lui => true
  | _ => false

def Riscv.hasSSADominance (_op : Riscv) (_index : Nat) : Bool :=
  true

#generate_dialect Riscv

instance : IsOpCode Riscv where
  fromName := Riscv.fromName
  name := Riscv.name
  propertiesOf := Riscv.propertiesOf
  fromAttrDict := Riscv.fromAttrDict
  toAttrDict := Riscv.toAttrDict

/--
Materialize register-valued fold results as `riscv.li`. A register always holds
64 bits, so the immediate is always an `i64`.
-/
def Riscv.materializeConstant {OpInfo : Type} [HasOpInfo OpInfo] [HasDialect OpInfo Riscv]
    (_op : Riscv) (value : RuntimeValue) (type : TypeAttr) : Option (Materialized OpInfo) :=
  match value, type.val with
  | .reg value, .registerType _ =>
    some (.of Riscv.li
      (RISCVImmediateProperties.mk (IntegerAttr.mk value.val.toInt (IntegerType.mk 64))))
  | _, _ => none

def OperationPtr.verifyRISCVimm12 {OpInfo : Type} [IsOpCode OpInfo]
    (op : OperationPtr) (ctx : WfIRContext OpInfo) (opIn : op.InBounds ctx.raw)
    (operands results : Nat) (imm : Int) : Except String PUnit := do
  op.verifyPlainOpCounts ctx opIn operands results
  if imm < -2048 ∨ imm > 2047 then
    let instrName := String.fromUTF8! (IsOpCode.name (op.getOpType ctx.raw opIn))
    throw s!"{instrName} immediate out of bounds: must fit in a signed 12-bit field [-2048, 2047]"
  else
    pure ()

/--
Check that a shift-amount/bit-index immediate fits in an unsigned 5-bit field
`[0, 31]`.
-/
def OperationPtr.verifyRISCVuimm5 {OpInfo : Type} [IsOpCode OpInfo]
    (op : OperationPtr) (ctx : WfIRContext OpInfo) (opIn : op.InBounds ctx.raw)
    (imm : Int) : Except String PUnit := do
  op.verifyPlainOpCounts ctx opIn 1 1
  if imm < 0 ∨ imm > 31 then
    let instrName := String.fromUTF8! (IsOpCode.name (op.getOpType ctx.raw opIn))
    throw s!"{instrName} immediate out of bounds: must fit in an unsigned 5-bit field [0, 31]"
  else
    pure ()

/--
Check that a shift-amount/bit-index immediate fits in an unsigned 6-bit field
`[0, 63]`.
-/
def OperationPtr.verifyRISCVuimm6 {OpInfo : Type} [IsOpCode OpInfo]
    (op : OperationPtr) (ctx : WfIRContext OpInfo) (opIn : op.InBounds ctx.raw)
    (imm : Int) : Except String PUnit := do
  op.verifyPlainOpCounts ctx opIn 1 1
  if imm < 0 ∨ imm > 63 then
    let instrName := String.fromUTF8! (IsOpCode.name (op.getOpType ctx.raw opIn))
    throw s!"{instrName} immediate out of bounds: must fit in an unsigned 6-bit field [0, 63]"
  else
    pure ()

def OperationPtr.verifyRISCVneg {OpInfo : Type} [IsOpCode OpInfo]
    (op : OperationPtr) (ctx : WfIRContext OpInfo) (opIn : op.InBounds ctx.raw)
    (operands results : Nat) (imm : Int) : Except String PUnit := do
  op.verifyPlainOpCounts ctx opIn operands results
  if imm < 0 ∨ 1048575 < imm then
    let instrName := String.fromUTF8! (IsOpCode.name (op.getOpType ctx.raw opIn))
    throw s!"{instrName} immediate out of bounds: must fit in an unsigned 20-bit field."
  else
    pure ()

/-- Ensure that every operand and result has type `!riscv.reg`. -/
def OperationPtr.verifyRISCVRegisterTypes {OpInfo : Type} [IsOpCode OpInfo]
    (op : OperationPtr) (ctx : WfIRContext OpInfo)
    (opIn : op.InBounds ctx.raw) : Except String PUnit := do
  let instrName := String.fromUTF8! (IsOpCode.name (op.getOpType ctx.raw opIn))
  let opTypes := op.getOperandTypes! ctx.raw
  for i in [0:opTypes.size] do
    match (opTypes[i]!).val with
    | .registerType _ => pure ()
    | _ => throw s!"{instrName}: Expected operand {i} to have !riscv.reg type"
  for i in [0:op.getNumResults ctx.raw opIn] do
    match ((op.getResult i).get! ctx.raw).type.val with
    | .registerType _ => pure ()
    | _ => throw s!"{instrName}: Expected result {i} to have !riscv.reg type"

/--
Verify the local invariants of a `riscv` operation in any operation-info type
containing the `riscv` dialect.
-/
def Riscv.verifyLocalInvariants {OpInfo : Type} [IsOpCode OpInfo]
    [HasDialect OpInfo Riscv] (opType : Riscv) (op : OperationPtr)
    (ctx : WfIRContext OpInfo) (opIn : op.InBounds ctx.raw) : Except String PUnit := do
  op.verifyRISCVRegisterTypes ctx opIn
  match opType with
  | .li => do
    op.verifyPlainOpCounts ctx opIn 0 1
    pure ()
  | .lui => do
    op.verifyRISCVneg ctx opIn 0 1 (op.getProperties! ctx.raw Riscv.lui).value.value
    pure ()
  | .auipc => do
    op.verifyRISCVneg ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.auipc).value.value
    pure ()
  | .addi => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.addi).value.value
    pure ()
  | .slti => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.slti).value.value
    pure ()
  | .sltiu => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.sltiu).value.value
    pure ()
  | .andi => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.andi).value.value
    pure ()
  | .ori => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.ori).value.value
    pure ()
  | .xori => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.xori).value.value
    pure ()
  | .addiw => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.addiw).value.value
    pure ()
  | .slli => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.slli).value.value
    pure ()
  | .srli => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.srli).value.value
    pure ()
  | .srai => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.srai).value.value
    pure ()
  | .add | .sub | .sll | .slt | .sltu
  | .xor | .srl | .sra | .or | .and => do
    op.verifyPlainOpCounts ctx opIn 2 1
    pure ()
  | .slliw => do
    op.verifyRISCVuimm5 ctx opIn (op.getProperties! ctx.raw Riscv.slliw).value.value
    pure ()
  | .srliw => do
    op.verifyRISCVuimm5 ctx opIn (op.getProperties! ctx.raw Riscv.srliw).value.value
    pure ()
  | .sraiw => do
    op.verifyRISCVuimm5 ctx opIn (op.getProperties! ctx.raw Riscv.sraiw).value.value
    pure ()
  | .addw | .subw | .sllw | .srlw | .sraw
  | .rem | .remu | .remw | .remuw
  | .mul | .mulh | .mulhu | .mulhsu | .mulw
  | .div | .divw | .divu | .divuw
  | .adduw | .sh1adduw | .sh2adduw | .sh3adduw
  | .sh1add | .sh2add | .sh3add => do
    op.verifyPlainOpCounts ctx opIn 2 1
    pure ()
  | .slliuw => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.slliuw).value.value
    pure ()
  | .andn | .orn | .xnor
  | .max | .maxu | .min | .minu
  | .rol | .ror | .rolw | .rorw => do
    op.verifyPlainOpCounts ctx opIn 2 1
    pure ()
  | .sextb | .sexth | .zexth
  | .clz | .clzw | .ctz | .ctzw
  | .cpop | .cpopw | .orcb | .rev8 => do
    op.verifyPlainOpCounts ctx opIn 1 1
    pure ()
  | .roriw => do
    op.verifyRISCVuimm5 ctx opIn (op.getProperties! ctx.raw Riscv.roriw).value.value
    pure ()
  | .rori => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.rori).value.value
    pure ()
  | .bclr | .bext | .binv | .bset => do
    op.verifyPlainOpCounts ctx opIn 2 1
    pure ()
  | .bclri => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.bclri).value.value
    pure ()
  | .bexti => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.bexti).value.value
    pure ()
  | .binvi => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.binvi).value.value
    pure ()
  | .bseti => do
    op.verifyRISCVuimm6 ctx opIn (op.getProperties! ctx.raw Riscv.bseti).value.value
    pure ()
  | .pack | .packh | .packw
  | .czeroeqz | .czeronez => do
    op.verifyPlainOpCounts ctx opIn 2 1
    pure ()
  | .ld => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.ld).value.value
    pure ()
  | .lw => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lw).value.value
    pure ()
  | .lwu => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lwu).value.value
    pure ()
  | .lh => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lh).value.value
    pure ()
  | .lhu => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lhu).value.value
    pure ()
  | .lb => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lb).value.value
    pure ()
  | .lbu => do
    op.verifyRISCVimm12 ctx opIn 1 1 (op.getProperties! ctx.raw Riscv.lbu).value.value
    pure ()
  | .sd => do
    op.verifyRISCVimm12 ctx opIn 2 0 (op.getProperties! ctx.raw Riscv.sd).value.value
    pure ()
  | .sw => do
    op.verifyRISCVimm12 ctx opIn 2 0 (op.getProperties! ctx.raw Riscv.sw).value.value
    pure ()
  | .sh => do
    op.verifyRISCVimm12 ctx opIn 2 0 (op.getProperties! ctx.raw Riscv.sh).value.value
    pure ()
  | .sb => do
    op.verifyRISCVimm12 ctx opIn 2 0 (op.getProperties! ctx.raw Riscv.sb).value.value
    pure ()
  | .mv | .not | .neg | .negw | .sextw
  | .zextb | .zextw | .seqz | .snez
  | .sltz | .sgtz => do
    op.verifyPlainOpCounts ctx opIn 1 1
    pure ()

instance : HasOpInfo Riscv where
  verifyLocalInvariants := Riscv.verifyLocalInvariants
  getEffects := Riscv.getEffects
  isConstantLike := Riscv.isConstantLike
  hasSSADominance := Riscv.hasSSADominance

end

end Veir
